*----------------------------------------------------------------------*
* Program Id:     ZVENDOR_INBOUND_PAYMENT_F53                              *
* Program Title:  Pollenware – Interface for posting inbound payment   *
*                 details using F-53                                   *
* Program Type:   Executable program                                   *
* Author:         Avinash Ravipati, Advent Global Solutions            *
* SAP Release:    ECC 6.0                                              *
* CR No:                                                               *
* Date:           11/05/2011                                           *
* Description:    This program is to post the inbound transaction      *
*                 details by using Transaction code F-53               *
*----------------------------------------------------------------------*
* CHANGE LOG                                                           *
* CR Number       Date                                Author           *
*----------------------------------------------------------------------*

REPORT  zvendor_inbound_payment MESSAGE-ID zpollenware.

* Include for data declarations.
INCLUDE zvendor_inbound_payment_f53t.

*----------------------------------------------------------------------*
*  EVENT  INITIALIZATION                                               *
*----------------------------------------------------------------------*
INITIALIZATION.
* getting timestamp.
  PERFORM extract_startup.

* validating user authorization to execute outbound extract.
  PERFORM validate_user_authorization.

* getting quickpay config details.
  PERFORM fetch_config_details.

* fetch error file path on application server
  PERFORM fetch_error_file_path.

*----------------------------------------------------------------------*
*  EVENT  SELECTION-SCREEN OUTPUT                                      *
*----------------------------------------------------------------------*

AT SELECTION-SCREEN OUTPUT.

* modifing the selection screen based on quickpay configuration
  PERFORM modify_screen.


*----------------------------------------------------------------------*
*  EVENT  SELECTION-SCREEN ON VALUE-REQUEST                            *
*----------------------------------------------------------------------*

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_dfile.

* f4 help for inbound file
  PERFORM browse_for_file .

*----------------------------------------------------------------------*
*  EVENT  SELECTION-SCREEN                                             *
*----------------------------------------------------------------------*

AT SELECTION-SCREEN.

* to validate error file application server path
  PERFORM check_path_appserver USING p_efpath.

* to validate log file application server path
  PERFORM check_path_appserver USING p_lfpath.

*----------------------------------------------------------------------*
*  EVENT  AT SELECTION-SCREEN                                          *
*----------------------------------------------------------------------*

START-OF-SELECTION.

* checking input values for PAI
  PERFORM check_input_pai.

* fetching data from source based on quickpay configuration
  PERFORM fetch_data_from_source.

* formatting the input data
  PERFORM format_data.

* inbound trasaction posting
  PERFORM fill_bdcdata.

*----------------------------------------------------------------------*
*  EVENT  END-OF-SELECTION                                             *
*----------------------------------------------------------------------*

END-OF-SELECTION.

* Create log file on app server at given path.
  PERFORM create_inbound_log.

* Central LOG file on Application Server.
  PERFORM create_appserver_log.

*----------------------------------------------------------------------*
*  Form  FTP_CONNECTION_OPEN                                           *
*----------------------------------------------------------------------*
*  Subroutine is for getting the password encryption string            *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM ftp_connection_open.

* Local work variables..................................................
  DATA: lv_slen TYPE i,                " String length
        lv_string TYPE string,         " text string
        lv_key TYPE i VALUE 26101957.  " Key

  CONCATENATE c_ftp
              gv_host
         INTO gv_ftp_path.

  lv_slen = STRLEN( gv_pass ).

  CALL FUNCTION 'HTTP_SCRAMBLE'
    EXPORTING
      SOURCE      = gv_pass
      sourcelen   = lv_slen
      key         = lv_key
    IMPORTING
      destination = gv_pass.

  CALL FUNCTION 'FTP_CONNECT'
    EXPORTING
      user            = gv_user
      password        = gv_pass
      host            = gv_host
      rfc_destination = gv_rfc
    IMPORTING
      handle          = gv_hdl.
*    EXCEPTIONS
*      not_connected   = 1
*      OTHERS          = 2.

  IF gv_hdl EQ 0.
    MESSAGE i010 WITH gv_ftp_path.
    lv_string = text-ucf.
    REPLACE ALL OCCURRENCES OF c_p1
                            IN lv_string
                          WITH gv_ftp_path.
* filling the process details for extract log
    PERFORM fill_process_details USING lv_string.
    CLEAR lv_string.

    gv_status = text-fal.
* LOG file on Application Server.
    PERFORM create_appserver_log.
    LEAVE PROGRAM.
  ENDIF.                               " IF GV_HD1 EQ 0.

  IF sy-subrc EQ 0.
    MESSAGE s009 WITH gv_ftp_path.
    lv_string = text-scf.
    REPLACE ALL OCCURRENCES OF c_p1
                            IN lv_string
                          WITH gv_ftp_path.
* filling the process details for extract log
    PERFORM fill_process_details USING lv_string.
    CLEAR lv_string.
  ELSE.
    MESSAGE i010 WITH gv_ftp_path.
    lv_string = text-ucf.
    REPLACE ALL OCCURRENCES OF c_p1
                            IN lv_string
                          WITH gv_ftp_path.
* filling the process details for extract log
    PERFORM fill_process_details USING lv_string.
    CLEAR lv_string.

    gv_status = text-fal.
* LOG file on Application Server.
    PERFORM create_appserver_log.
    LEAVE PROGRAM.
  ENDIF.                               " IF SY-SUBRC EQ 0

ENDFORM.                               " FTP_CONNECTION_OPEN

*----------------------------------------------------------------------*
*  Form  FETCH_FILE_FROM_FTP                                           *
*----------------------------------------------------------------------*
*  Subroutine is for fetching the file from                            *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM fetch_file_from_ftp.

* Local work variables..................................................
  DATA: lv_flag TYPE char1
               VALUE c_flag_off,       " Flag
        lv_length TYPE i,              " String length
        lv_relength TYPE i,            " File name length
        lv_string TYPE string,         " Text string
        lv_char TYPE char1,            " Character
        lv_ser_name TYPE msxxlist-name." Server name

* Local field strings...................................................
  DATA: BEGIN OF ls_result,
          line(100) TYPE c,            " string
        END OF ls_result.
* Local tables .........................................................
  DATA lt_result LIKE STANDARD TABLE
                   OF ls_result.

  IF gv_cmd_ftppath IS NOT INITIAL.
    lv_string = gv_cmd_ftppath+0(1).
    IF lv_string NE c_back_slash.
      CONCATENATE c_back_slash
                  gv_cmd_ftppath
             INTO gv_cmd_ftppath.
    ENDIF.                             " IF LV_STRING NE C_BACK_SLASH.

    CONCATENATE gv_cmd_cd
                gv_cmd_ftppath
           INTO gv_cmd
   SEPARATED BY space.

    REPLACE ALL OCCURRENCES OF c_back_slash
                            IN gv_cmd_ftppath
                          WITH c_farw_slash.
    CONCATENATE c_ftp
                gv_host
                gv_cmd_ftppath
           INTO gv_ftp_path.

    PERFORM ftp_command CHANGING lv_flag
                               lt_result.
    REFRESH lt_result.
    CLEAR: lv_flag,
           lv_string.

  ENDIF.                               " GV_CMD_FTPPATH IS NOT INITIAL.

  gv_cmd = gv_cmd_ascii.

  CLEAR lv_flag.

  PERFORM ftp_command CHANGING lv_flag
                               lt_result.
  CLEAR lv_flag.
  REFRESH lt_result.

  gv_cmd = gv_cmd_dir.

  PERFORM ftp_command CHANGING lv_flag
                               lt_result.

  IF lv_flag EQ c_flag_on.
    LOOP AT lt_result INTO ls_result FROM 4.
      lv_char = ls_result+0(1).
      IF lv_char EQ c_hyphen.
        IF ls_result-line CS c_award.
          lv_length = STRLEN( ls_result-line ).
          lv_relength = lv_length - 49.
          IF p_fname IS INITIAL.
            p_fname = ls_result-line+49(lv_relength).
          ENDIF.                       " IF P_FNAME IS INITIAL.
          EXIT.
        ELSE.
          lv_string = text-anf.
          REPLACE ALL OCCURRENCES OF c_p1
                                  IN lv_string
                                WITH gv_ftp_path.
* filling the process details for extract log
          PERFORM fill_process_details USING lv_string.
          gv_status = text-fal.

* LOG file on Application Server.
          PERFORM create_appserver_log.

          MESSAGE e026 WITH gv_ftp_path.
          CLEAR lv_string.
        ENDIF.                         " IF LS_RESULT-LINE CS C_CSV.
      ENDIF.                           " IF LV_CHAR EQ C_HYPHEN.
    ENDLOOP.                           " LOOP AT LT_RESULT INTO ...
    IF p_fname IS INITIAL.
      lv_string = text-anf.
      REPLACE ALL OCCURRENCES OF c_p1
                              IN lv_string
                            WITH gv_ftp_path.
* filling the process details for extract log
      PERFORM fill_process_details USING lv_string.
      gv_status = text-fal.

* LOG file on Application Server.
      PERFORM create_appserver_log.

      MESSAGE e026 WITH gv_ftp_path.
      CLEAR lv_string.
    ENDIF.                             " IF P_FNAME IS INITIAL.
  ENDIF.                               " IF LV_FLAG EQ C_FLAG_ON.
  REFRESH lt_result.

  CONCATENATE gv_cmd_lcd
              gv_ftp_app_path
         INTO gv_cmd SEPARATED BY space.

  CLEAR lv_flag.

  PERFORM ftp_command CHANGING lv_flag
                               lt_result.
  REFRESH lt_result.

  CONCATENATE gv_cmd_get
              p_fname
         INTO gv_cmd SEPARATED BY space.

  CLEAR lv_flag.

  PERFORM ftp_command CHANGING lv_flag
                               lt_result.
  REFRESH lt_result.

  IF lv_flag EQ c_flag_on.
    MESSAGE s011 WITH p_fname gv_ftp_path.

    CALL FUNCTION 'GENERAL_GET_APP_SERVER_NAME'
      IMPORTING
        server_name = lv_ser_name.

    MESSAGE s013 WITH lv_ser_name.

    lv_string = text-stf.
    REPLACE ALL OCCURRENCES OF c_p1
                            IN lv_string
                          WITH lv_ser_name.

* filling the process details for extract log
    PERFORM fill_process_details USING lv_string.
    CLEAR lv_string.
  ELSE.

    lv_string = text-uff.

    REPLACE ALL OCCURRENCES OF c_p1
                            IN lv_string
                          WITH p_fname.

    REPLACE ALL OCCURRENCES OF c_p2
                            IN lv_string
                          WITH gv_ftp_path.

* filling the process details for extract log
    PERFORM fill_process_details USING lv_string.
    gv_status = text-fal.

* LOG file on Application Server.
    PERFORM create_appserver_log.

    MESSAGE i012 WITH p_fname gv_ftp_path.
    LEAVE PROGRAM.

  ENDIF.                               " IF LV_FLAG EQ C_FLAG_ON

ENDFORM.                               " FETCH_FILE_FROM_FTP

*----------------------------------------------------------------------*
*  Form  FTP_COMMAND                                                   *
*----------------------------------------------------------------------*
*  Subroutine is for ftp connection and fetch file from ftp            *
*----------------------------------------------------------------------*
*  <-- cv_flag  -  flag variable                                       *
*----------------------------------------------------------------------*
FORM ftp_command CHANGING cv_flag TYPE char1
                          cv_table TYPE STANDARD TABLE.


  cv_flag = c_flag_off.

  IF gv_cmd NE space.

    CALL FUNCTION 'FTP_COMMAND'
      EXPORTING
        handle                = gv_hdl
        command               = gv_cmd
        compress              = gv_compress
        rfc_destination       = gv_rfc
*     VERIFY                =
*   IMPORTING
*     FILESIZE              =
*     FILEDATE              =
*     FILETIME              =
      TABLES
        data                  = cv_table
      EXCEPTIONS
        tcpip_error           = 1
        command_error         = 2
        data_error            = 3
        OTHERS                = 4
              .
    IF sy-subrc EQ 0.
      cv_flag = c_flag_on.
    ENDIF.                             " IF SY-SUBRC EQ 0

    CLEAR gv_cmd.

  ENDIF.                               " IF GV_CMD NE SPACE.

ENDFORM.                               " FTP_COMMAND

*----------------------------------------------------------------------*
*  Form  FTP_CONNECTION_CLOSE                                          *
*----------------------------------------------------------------------*
*  Subroutine is for disconnect and close the ftp connection           *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM ftp_connection_close .

  CALL FUNCTION 'FTP_DISCONNECT'
    EXPORTING
      handle = gv_hdl.

  CALL FUNCTION 'RFC_CONNECTION_CLOSE'
    EXPORTING
      destination = gv_rfc.

ENDFORM.                               " FTP_CONNECTION_CLOSE

*----------------------------------------------------------------------*
*  Form  VALIDATE_USER_AUTHORIZATION                                   *
*----------------------------------------------------------------------*
*  Subroutine for validating user authorization to execute outbound    *
*  extract.                                                            *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM validate_user_authorization.

  AUTHORITY-CHECK OBJECT 'ZVEND_INB' FOR USER sy-uname
                      ID 'ACTVT' FIELD '01'.
  CLEAR gs_process_details.

  IF sy-subrc NE 0.

* filling the process details for extract log
    PERFORM fill_process_details USING text-uaf.
    gv_status = text-fua.

* LOG file on Application Server.
    PERFORM create_appserver_log.

    MESSAGE i000.
    LEAVE PROGRAM.
  ELSE.
* filling the process details for extract log
    PERFORM fill_process_details USING text-uas.
  ENDIF.                               " IF SY_SUBRC NE 0

ENDFORM.                               " VALIDATE_USER_AUTHORIZATION

*----------------------------------------------------------------------*
*  Form  FETCH_DATA_FROM_SERVER                                        *
*----------------------------------------------------------------------*
*  This subroutine is for fetching the file data from app server       *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM fetch_data_from_server .

* Local work variables..................................................
  DATA:
    lv_subrc TYPE sysubrc,             " Return Value of ABAP Statements
    lv_string TYPE string,             " Text string
    lv_sreccount TYPE char10,          " Number of records
    lv_reccount TYPE i,                " Number of records
    lv_length TYPE i,                  " Length
    lv_char TYPE char1,                " Character
    lv_slash TYPE char1,               " Slash type in path
    lv_name TYPE salfile-longname.     " App server file path

  DATA:
    lt_filelist TYPE STANDARD TABLE OF
                              salfldir." Directory of Files

  DATA:
    ls_filelist LIKE LINE OF lt_filelist.
  " Field string of directory files

  IF gv_as_path IS NOT INITIAL.
    lv_length = STRLEN( gv_as_path ).
    lv_length = lv_length - 1.
    lv_char = gv_as_path+lv_length(1).

    IF p_fname IS INITIAL.
      lv_name = gv_as_path.

      CALL FUNCTION 'RZL_READ_DIR_LOCAL'
        EXPORTING
          name           = lv_name
        TABLES
          file_tbl       = lt_filelist
        EXCEPTIONS
          argument_error = 1
          not_found      = 2
          OTHERS         = 3.
      IF sy-subrc EQ 0.
        IF lt_filelist IS NOT INITIAL.
          READ TABLE lt_filelist INTO ls_filelist INDEX 1.
          LOOP AT lt_filelist INTO ls_filelist FROM 3.
            IF ls_filelist-name CS c_award.
              p_fname = ls_filelist-name.
              EXIT.
            ENDIF.                     " IF ls_filelist-name CS c_award.
          ENDLOOP.                     " LOOP AT lt_filelist into ...
          IF p_fname IS INITIAL.
            lv_string = text-ans.
            REPLACE ALL OCCURRENCES OF c_p1
                                    IN lv_string
                                  WITH gv_as_path.
* filling the process details for extract log
            PERFORM fill_process_details USING lv_string.
            gv_status = text-fal.

* LOG file on Application Server.
            PERFORM create_appserver_log.

            MESSAGE e027 WITH gv_as_path.
            CLEAR lv_string.
          ENDIF.                       " IF P_FNAME IS INITIAL.
        ELSE.
          lv_string = text-ans.
          REPLACE ALL OCCURRENCES OF c_p1
                                  IN lv_string
                                WITH gv_as_path.
* filling the process details for extract log
          PERFORM fill_process_details USING lv_string.
          gv_status = text-fal.

* LOG file on Application Server.
          PERFORM create_appserver_log.

          MESSAGE e027 WITH gv_as_path.
          CLEAR lv_string.
        ENDIF.                         " IF LT_FILELIST IS NOT initial
      ELSE.
        lv_string = text-api.
        REPLACE ALL OCCURRENCES OF c_p1
                                IN lv_string
                              WITH gv_as_path.
* filling the process details for extract log
        PERFORM fill_process_details USING lv_string.
        gv_status = text-fal.

* LOG file on Application Server.
        PERFORM create_appserver_log.

        MESSAGE e021 WITH gv_as_path.
        CLEAR lv_string.
      ENDIF.                           " IF SY-SUBRC EQ 0.
    ENDIF.                             " IF P_FNAME IS INITIAL.

    IF gv_as_path CA c_farw_slash.
      lv_slash = c_farw_slash.
    ELSE.
      lv_slash = c_back_slash.
    ENDIF.                             " IF P_DPATH CA C_FARW_SLASH.

    IF lv_char EQ lv_slash.
      CONCATENATE gv_as_path
                  p_fname
             INTO gv_dataset.
    ELSE.
      CONCATENATE gv_as_path
                  lv_slash
                  p_fname
             INTO gv_dataset.
    ENDIF.                             " IF LV_CHAR EQ LV_SLASH.
  ELSE.
    gv_dataset = p_fname.
  ENDIF.                               " IF GV_AS_PATH IS NOT INITIAL.

  OPEN DATASET gv_dataset FOR INPUT IN TEXT MODE ENCODING DEFAULT.

  IF sy-subrc NE 0.

* filling the process details for extract log
    PERFORM fill_process_details USING text-uaa.

    gv_status = text-fal.

* LOG file on Application Server.
    PERFORM create_appserver_log.

    MESSAGE e030.

  ELSE.
    IF gv_as EQ c_flag_on.
      lv_string = text-apv.
      REPLACE ALL OCCURRENCES OF c_p1
                              IN lv_string
                            WITH gv_dataset.
* filling the process details for extract log
      PERFORM fill_process_details USING lv_string.
      CLEAR lv_string.
    ENDIF.                             " IF GV_AS EQ C_FLAG_ON.
  ENDIF.                               " IF SY-SUBRC NE 0

*  READ DATASET gv_dataset INTO gs_file_data.

  DO.
    READ DATASET gv_dataset INTO gs_file_data.
    lv_subrc = sy-subrc.
    IF lv_subrc NE 0.
      EXIT.
    ENDIF.                             " IF SY-SUBRC NE 0

    SPLIT gs_file_data AT c_comma
                     INTO gs_payment_data-pbid
                          gs_payment_data-pbnam
                          gs_payment_data-xblnr
                          gs_payment_data-lifnr
                          gs_payment_data-bsnam
                          gs_payment_data-belnr
                          gs_payment_data-invid
                          gs_payment_data-bktxt
                          gs_payment_data-wrbtr
                          gs_payment_data-basid
                          gs_payment_data-sgtxt
                          gs_payment_data-disc
                          gs_payment_data-inco
                          gs_payment_data-oamt
                          gs_payment_data-bldat
                          gs_payment_data-pterm
                          gs_payment_data-waers
                          gs_payment_data-payamt
                          gs_payment_data-valut
                          gs_payment_data-doctyp
                          gs_payment_data-bmtid
                          gs_payment_data-btde
                          gs_payment_data-sgrp
                          gs_payment_data-zuonr
                          gs_payment_data-hbank.
    IF sy-subrc EQ 0.
      APPEND gs_payment_data TO gt_payment_data.
      CLEAR gs_payment_data.
    ELSE.
* filling the process details for extract log
      PERFORM fill_process_details USING text-afi.

      gv_status = text-fal.
* LOG file on Application Server.
      PERFORM create_appserver_log.
      MESSAGE e029.
    ENDIF.                             " IF SY-SUBRC EQ 0.
  ENDDO.                               " IF SY-SUBRC NE 0 EXIT

  CLOSE DATASET gv_dataset.

  DESCRIBE TABLE gt_payment_data LINES lv_reccount.

  lv_reccount = lv_reccount - 1.

  MESSAGE s014 WITH lv_reccount p_fname.

  lv_string = text-nrf.

  lv_sreccount = lv_reccount.

  CONDENSE lv_sreccount NO-GAPS.
  REPLACE ALL OCCURRENCES OF c_p1
                          IN lv_string
                        WITH lv_sreccount.

  REPLACE ALL OCCURRENCES OF c_p2
                          IN lv_string
                        WITH p_fname.

* filling the process details for extract log
  PERFORM fill_process_details USING lv_string.

ENDFORM.                               " FETCH_DATA_FROM_SERVER

*----------------------------------------------------------------------*
*  Form  FILL_BDCDATA                                                  *
*----------------------------------------------------------------------*
*  This subroutine is for posting inbound trasactions                  *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM fill_bdcdata .

* Local work variables..................................................
  DATA: lv_invid TYPE rf05a-sel01,     " Invoice ID
        lv_lifnr TYPE lfb1-lifnr,      " Vendor Number
        lv_hbank TYPE char13,          " House bank
        lv_fname TYPE string,          " String
        lv_row TYPE char2,             " Row number
        lv_msg TYPE string,            " message
        lv_error_lines TYPE i.         " of Error line

* Local table declarations..............................................
  DATA:
        lt_uniref TYPE SORTED TABLE
                    OF typ_uniref
       WITH UNIQUE KEY invid
          INITIAL SIZE 0.

  DATA:
        lt_uniinv TYPE STANDARD TABLE
                    OF typ_uniinv
          INITIAL SIZE 0.

  DATA:
        lt_refdis TYPE STANDARD TABLE
                    OF typ_dis
          INITIAL SIZE 0.

  DATA:
        lt_invdis TYPE STANDARD TABLE
                    OF typ_dis
          INITIAL SIZE 0.

* Local field string declarations.......................................
  DATA:
    ls_uniref TYPE typ_uniref.

  DATA:
    ls_refdis TYPE typ_dis.

  DATA:
    ls_uniinv TYPE typ_uniinv.

  DATA:
    ls_invdis TYPE typ_dis.

  LOOP AT gt_payment_bdcheader INTO gs_payment_bdcheader.
    REFRESH gt_bdc_data.

    PERFORM bdc_dynpro      USING 'SAPMF05A'
                                  '0103'.
    PERFORM bdc_field       USING 'BDC_CURSOR'
                                  'RF05A-XPOS1(06)'.
    PERFORM bdc_field       USING 'BDC_OKCODE'
                                  '/00'.
    PERFORM bdc_field       USING 'BKPF-BLDAT'
                                  gs_payment_bdcheader-bldat.
    " Adjusted Payment Date

    PERFORM bdc_field       USING 'BKPF-BLART'
                                  gs_payment_bdcheader-blart.
    " Document type

    PERFORM bdc_field       USING 'BKPF-BUKRS'
                                  gs_payment_bdcheader-bukrs.
    " Company code

    PERFORM bdc_field       USING 'BKPF-BUDAT'
                                  gs_payment_bdcheader-budat.
    " Posting date

    PERFORM bdc_field       USING 'BKPF-MONAT'
                                  ''.  " Posting period

    PERFORM bdc_field       USING 'BKPF-WAERS'
                                  gs_payment_bdcheader-waers.
    " Currency key

    PERFORM bdc_field       USING 'BKPF-XBLNR'
                                  gs_payment_bdcheader-xblnr.
    " Pollenware Vendor ID

    PERFORM bdc_field       USING 'BKPF-BKTXT'
                                  gs_payment_bdcheader-bktxt.
    " Event ID

    PERFORM bdc_field       USING 'RF05A-KONTO'
                                  gs_payment_bdcheader-ukont.
    " Account

    PERFORM bdc_field       USING 'BSEG-WRBTR'
                                  gs_payment_bdcheader-wrbtr.
    " Amount

    PERFORM bdc_field       USING 'BSEG-VALUT'
                                  gs_payment_bdcheader-valut.
    " Original Invoice Due Date

    PERFORM bdc_field       USING 'BSEG-SGTXT'
                                  gs_payment_bdcheader-sgtxt.
    " Item text

    PERFORM bdc_field       USING 'BSEG-ZUONR'
                                  gs_payment_bdcheader-zuonr.
    " Assignment

    PERFORM bdc_field       USING 'RF05A-AGKON'
                                  gs_payment_bdcheader-lifnr.
    " Vendor Account

    PERFORM bdc_field       USING 'RF05A-AGKOA'
                                  'K'.
    PERFORM bdc_field       USING 'RF05A-XNOPS'
                                  'X'.

    READ TABLE gt_invoice INTO gs_invoice
      WITH KEY lifnr = gs_payment_bdcheader-lifnr
               hbank = gs_payment_bdcheader-hbank.

    IF sy-subrc EQ 0.

      PERFORM bdc_field       USING 'RF05A-XPOS1(01)'
                                    ''.
      PERFORM bdc_field       USING 'RF05A-XPOS1(03)'
                                    'X'.

      PERFORM bdc_dynpro      USING 'SAPMF05A'
                                    '0731'.
      PERFORM bdc_field       USING 'BDC_CURSOR'
                                    'RF05A-SEL01(01)'.
      PERFORM bdc_field       USING 'BDC_OKCODE'
                                    '=PA'.

      LOOP AT gt_invoice INTO gs_invoice
                        WHERE lifnr = gs_payment_bdcheader-lifnr
                          AND hbank = gs_payment_bdcheader-hbank.

        CLEAR ls_uniinv.

        READ TABLE lt_uniinv INTO ls_uniinv
                   WITH TABLE KEY belnr = gs_invoice-belnr.

        IF sy-subrc NE 0.
          ls_uniinv-belnr = gs_invoice-belnr.
          APPEND ls_uniinv TO lt_uniinv.
        ENDIF.                         " IF SY-SUBRC NE 0

        CLEAR gs_invoice.

      ENDLOOP.                         " LOOP AT GT_INVOICE INTO GS_I...

      LOOP AT lt_uniinv INTO ls_uniinv.

        CLEAR: lv_fname,
               lv_row.

        IF sy-tabix < 10.
          lv_row = sy-tabix.
          CONCATENATE 'RF05A-SEL01(0'
                      lv_row
                      ')'
                 INTO lv_fname.
        ELSE.
          lv_row = sy-tabix.
          CONCATENATE 'RF05A-SEL01('
                      lv_row
                      ')'
                 INTO lv_fname.
        ENDIF.                         " IF SY-TABIX < 10.

        PERFORM bdc_field USING lv_fname
                                ls_uniinv-belnr.

      ENDLOOP.                         " LOOP AT LT_UNIINV INTO LS_UN...

      REFRESH lt_uniinv.

      PERFORM bdc_dynpro      USING 'SAPDF05X' '3100'.

      PERFORM bdc_field       USING 'BDC_OKCODE'
                                    '/00'.

      PERFORM bdc_field       USING 'BDC_CURSOR'
                                    'DF05B-PSSKT(01)'.
      PERFORM bdc_field       USING 'RF05A-ABPOS'
                                    '1'.
      LOOP AT gt_invoice INTO gs_invoice
                        WHERE lifnr = gs_payment_bdcheader-lifnr
                          AND hbank = gs_payment_bdcheader-hbank.

        CLEAR ls_invdis.

        ls_invdis-disc = gs_invoice-disc.

        APPEND ls_invdis TO lt_invdis.

        CLEAR gs_invoice.

      ENDLOOP.                         " LOOP AT GT_INVOICE INTO GS_I...

      LOOP AT lt_invdis INTO ls_invdis.

        CLEAR: lv_fname,
               lv_row.

        IF sy-tabix < 10.
          lv_row = sy-tabix.
          CONCATENATE 'DF05B-PSSKT(0'
                      lv_row
                      ')'
                 INTO lv_fname.
        ELSE.
          lv_row = sy-tabix.
          CONCATENATE 'DF05B-PSSKT('
                      lv_row
                      ')'
                 INTO lv_fname.
        ENDIF.                         " IF SY-TABIX < 10

        PERFORM bdc_field USING lv_fname
                                ls_invdis-disc.
        CLEAR ls_invdis.

      ENDLOOP.                         " LOOP AT LT_INVDIS INTO GS_IN...

      REFRESH lt_invdis.

      PERFORM bdc_dynpro      USING 'SAPDF05X'
                                    '3100'.

      PERFORM bdc_field       USING 'BDC_OKCODE'
                                    '=BS'.
      PERFORM bdc_field       USING 'BDC_CURSOR'
                                    'DF05B-PSSKT(01)'.
      PERFORM bdc_field       USING 'RF05A-ABPOS'
                                    '1'.
      PERFORM bdc_dynpro      USING 'SAPMF05A'
                                    '0700'.
      PERFORM bdc_field       USING 'BDC_CURSOR'
                                    'RF05A-NEWBS'.
      PERFORM bdc_field       USING 'BDC_OKCODE'
                                    '=BU'.
      PERFORM bdc_field       USING 'BKPF-XBLNR'
                                    gs_payment_bdcheader-xblnr.
      PERFORM bdc_field       USING 'BKPF-BKTXT'
                                    gs_payment_bdcheader-bktxt.

    ELSE.

      PERFORM bdc_field       USING 'RF05A-XPOS1(01)'
                                    ''.
      PERFORM bdc_field       USING 'RF05A-XPOS1(06)'
                                    'X'.

      READ TABLE gt_payment_bdcdata
            INTO gs_payment_bdcdata
        WITH KEY lifnr = gs_payment_bdcheader-lifnr
                 hbank = gs_payment_bdcheader-hbank
                 doctyp = c_cmemo_doctyp.

      IF sy-subrc EQ 0.

        lv_lifnr = gs_payment_bdcdata-lifnr.
        lv_invid = gs_payment_bdcdata-invid.
        lv_hbank = gs_payment_bdcdata-hbank.

        READ TABLE gt_payment_bdcdata
              INTO gs_payment_bdcdata
          WITH KEY lifnr = lv_lifnr
                   invid = lv_invid
                   hbank = lv_hbank
                   doctyp = c_inv_doctyp .

        IF sy-subrc NE 0.

          PERFORM bdc_dynpro      USING 'SAPMF05A'
                                        '0608'.
          PERFORM bdc_field       USING 'BDC_CURSOR'
                                        'RF05A-XPOS1(05)'.
          PERFORM bdc_field       USING 'BDC_OKCODE'
                                        '=ENTR'.
          PERFORM bdc_field       USING 'RF05A-XPOS1(01)'
                                        ''.
          PERFORM bdc_field       USING 'RF05A-XPOS1(07)'
                                        'X'.
          PERFORM bdc_dynpro      USING 'SAPMF05A'
                                        '0731'.
          PERFORM bdc_field       USING 'BDC_CURSOR'
                                        'RF05A-SEL01(01)'.
          PERFORM bdc_field       USING 'BDC_OKCODE'
                                        '=SL2'.
          PERFORM bdc_field       USING 'RF05A-SEL01(01)'
                                        'KG'.
          PERFORM bdc_dynpro      USING 'SAPMF05A'
                                        '0608'.
          PERFORM bdc_field       USING 'BDC_CURSOR'
                                        'RF05A-XPOS1(05)'.
          PERFORM bdc_field       USING 'BDC_OKCODE'
                                        '=ENTR'.

        ELSE.

          PERFORM bdc_dynpro      USING 'SAPMF05A'
                                        '0608'.
          PERFORM bdc_field       USING 'BDC_CURSOR'
                                        'RF05A-XPOS1(06)'.
          PERFORM bdc_field       USING 'BDC_OKCODE'
                                        '=ENTR'.

        ENDIF.                         " IF SY-SUBRC NE 0.

      ENDIF.                           " IF SY-SUBRC EQ 0.

      PERFORM bdc_field       USING 'RF05A-XPOS1(01)'
                                    ''.
      PERFORM bdc_field       USING 'RF05A-XPOS1(05)'
                                    'X'.
      PERFORM bdc_dynpro      USING 'SAPMF05A'
                                    '0731'.
      PERFORM bdc_field       USING 'BDC_CURSOR'
                                    'RF05A-SEL01(01)'.
      PERFORM bdc_field       USING 'BDC_OKCODE'
                                    '=PA'.

      LOOP AT gt_reference INTO gs_reference
                          WHERE lifnr = gs_payment_bdcheader-lifnr
                            AND hbank = gs_payment_bdcheader-hbank.

        CLEAR ls_uniref.

        READ TABLE lt_uniref INTO ls_uniref
                   WITH TABLE KEY invid = gs_reference-invid.

        IF sy-subrc NE 0.
          ls_uniref-invid = gs_reference-invid.
          INSERT ls_uniref INTO TABLE lt_uniref.
        ENDIF.                         " IF SY-SUBRC NE 0

        CLEAR gs_reference.

      ENDLOOP.                         " LOOP AT GT_REFERENCE INTO ...

      LOOP AT lt_uniref INTO ls_uniref.

        CLEAR: lv_fname,
               lv_row.

        IF sy-tabix < 10.
          lv_row = sy-tabix.
          CONCATENATE 'RF05A-SEL01(0'
                      lv_row
                      ')'
                 INTO lv_fname.
        ELSE.
          lv_row = sy-tabix.
          CONCATENATE 'RF05A-SEL01('
                      lv_row
                      ')'
                 INTO lv_fname.
        ENDIF.                         " IF SY-TABIX < 10

        PERFORM bdc_field       USING lv_fname
                                      ls_uniref-invid.
        " Invoice Id

        CLEAR ls_uniref.

      ENDLOOP.                         " LOOP AT LT_UNIREF INTO LS_...

      REFRESH lt_uniref.

      PERFORM bdc_dynpro      USING 'SAPDF05X'
                                    '3100'.
      PERFORM bdc_field       USING 'BDC_OKCODE'
                                    '/00'.
      PERFORM bdc_field       USING 'BDC_CURSOR'
                                    'DF05B-PSSKT(01)'.
      PERFORM bdc_field       USING 'RF05A-ABPOS'
                                    '1'.

      LOOP AT gt_reference INTO gs_reference
                          WHERE lifnr = gs_payment_bdcheader-lifnr
                            AND hbank = gs_payment_bdcheader-hbank.

        CLEAR ls_refdis.

        ls_refdis-disc = gs_reference-disc.

        IF gs_reference-doctyp EQ c_cmemo_doctyp.
          INSERT ls_refdis INTO lt_refdis INDEX 1.
        ELSE.
          APPEND ls_refdis TO lt_refdis.
        ENDIF.                         " IF DOCTYP EQ C_DMEMO_DOCTYP

      ENDLOOP.                         " LOOP AT GT_REFERENCE INTO ...


      LOOP AT lt_refdis INTO ls_refdis.

        CLEAR: lv_fname,
               lv_row.

        IF sy-tabix < 10.
          lv_row = sy-tabix.
          CONCATENATE 'DF05B-PSSKT(0'
                      lv_row
                      ')'
                 INTO lv_fname.
        ELSE.
          lv_row = sy-tabix.
          CONCATENATE 'DF05B-PSSKT('
                      lv_row
                      ')'
                 INTO lv_fname.
        ENDIF.                         " IF SY-TABIX < 10.

        PERFORM bdc_field USING lv_fname
                                ls_refdis-disc.

        CLEAR ls_refdis.

      ENDLOOP.                         " LOOP AT LT_REFDIS INTO LS_RE...

      REFRESH lt_refdis.

      PERFORM bdc_dynpro      USING 'SAPDF05X'
                                    '3100'.
      PERFORM bdc_field       USING 'BDC_OKCODE'
                                    '=BS'.
      PERFORM bdc_field       USING 'BDC_CURSOR'
                                    'DF05B-PSSKT(01)'.
      PERFORM bdc_field       USING 'RF05A-ABPOS'
                                    '1'.
      PERFORM bdc_dynpro      USING 'SAPMF05A'
                                    '0700'.
      PERFORM bdc_field       USING 'BDC_CURSOR'
                                    'RF05A-NEWBS'.
      PERFORM bdc_field       USING 'BDC_OKCODE'
                                    '=BU'.
      PERFORM bdc_field       USING 'BKPF-XBLNR'
                                    gs_payment_bdcheader-xblnr.
      " Pollenware Vendor ID
      PERFORM bdc_field       USING 'BKPF-BKTXT'
                                    gs_payment_bdcheader-bktxt.
      " Event ID

    ENDIF.                             " IF SY-SUBRC EQ 0.

    REFRESH gt_bdc_messages.

    CALL TRANSACTION 'F-53' USING gt_bdc_data
                             MODE 'N'
                           UPDATE 'A'
                         MESSAGES INTO gt_bdc_messages.

    IF sy-subrc NE 0.
      APPEND gs_payment_bdcheader TO gt_error_data.
    ELSE.
      CLEAR gs_success_bdc.

      gs_success_bdc-lifnr = gs_payment_bdcheader-lifnr.
      gs_success_bdc-hbank = gs_payment_bdcheader-hbank.

      APPEND gs_success_bdc TO gt_success_bdc.
    ENDIF.                             " IF SY-SUBRC NE 0.

    LOOP AT gt_bdc_messages INTO gs_bdc_messages.
      APPEND gs_bdc_messages TO gt_messages.
    ENDLOOP.                           " LOOP AT GT_BDC_MESSAGES INTO ..

  ENDLOOP.                             " LOOP AT GT_PAYMENT_BDCHEADER ..

  DESCRIBE TABLE gt_error_data LINES lv_error_lines.

  IF lv_error_lines GT 0.
* saving failed records to app server.
    PERFORM save_erecords_appserver.
* creating session method for failed transactions.
    PERFORM create_session_erecords.
  ELSE.
    gv_status = text-suc.
  ENDIF.                               " IF LV_ERROR_LINES GE 0

  LOOP AT gt_messages INTO gs_messages.
    CALL FUNCTION 'FORMAT_MESSAGE'
      EXPORTING
        id        = gs_messages-msgid
        lang      = sy-langu
        no        = gs_messages-msgnr
        v1        = gs_messages-msgv1
        v2        = gs_messages-msgv2
        v3        = gs_messages-msgv3
        v4        = gs_messages-msgv4
      IMPORTING
        msg       = lv_msg
      EXCEPTIONS
        not_found = 1
        OTHERS    = 2.

    IF sy-subrc EQ 0.
      WRITE:/ lv_msg.
    ENDIF.                             " IF SY-SUBRC EQ 0
  ENDLOOP.                             " LOOP AT GT_BDC_MESSAGES INTO ..
ENDFORM.                               " FILL_BDCDATA

*----------------------------------------------------------------------*
*  Form  BDC_DYNPRO                                                    *
*----------------------------------------------------------------------*
*  This subroutine is for Start new screen                             *
*----------------------------------------------------------------------*
*  --> iv_program  -  program name                                     *
*  --> iv_dynpro   -  screen number                                    *
*----------------------------------------------------------------------*
FORM bdc_dynpro USING iv_program TYPE bdcdata-program
                      iv_dynpro TYPE bdcdata-dynpro.
  CLEAR gs_bdc_data.
  gs_bdc_data-program  = iv_program.
  gs_bdc_data-dynpro   = iv_dynpro.
  gs_bdc_data-dynbegin = 'X'.
  APPEND gs_bdc_data TO gt_bdc_data.
ENDFORM.                               " BDC_DYNPRO

*----------------------------------------------------------------------*
*  Form  BDC_FIELD                                                     *
*----------------------------------------------------------------------*
*  This subroutine is for Insert field                                 *
*----------------------------------------------------------------------*
*  --> iv_fnam  -  field name                                          *
*  --> iv_fval  -  field value                                         *
*----------------------------------------------------------------------*
FORM bdc_field USING iv_fnam TYPE any
                     iv_fval TYPE any.
  CLEAR gs_bdc_data.
  gs_bdc_data-fnam = iv_fnam.
  gs_bdc_data-fval = iv_fval.
  APPEND gs_bdc_data TO gt_bdc_data.
ENDFORM.                               " BDC_FIELD

*----------------------------------------------------------------------*
*  Form  FORMAT_DATA                                                   *
*----------------------------------------------------------------------*
*  This subroutine is for formatting the input data                    *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM format_data .

* Local work variables..................................................
  DATA: lv_belnr TYPE bkpf-belnr,      " Invoice number
        lv_gjahr TYPE bkpf-gjahr,      " Fiscal year
        lv_wrbtr TYPE bseg-wrbtr,      " Amount
        lv_index TYPE sy-tabix,        " tabix
        lv_hbkid TYPE t042i-hbkid,     " Bank Key
        lv_hktid TYPE t042i-hktid,     " ID for account details
        lv_country TYPE t001-land1,    " Country code
        lv_pmt_mtd TYPE t042z-zlsch,   " Payment method
        lv_mtd_name TYPE t042z-text1,  " Payment method description
        lv_string TYPE string,         " String
        lv_doctype type char18.        " Document Type

  CLEAR: gv_tot_amt,
         gv_tot_dis.

  LOOP AT gt_payment_data INTO gs_payment_data.

    CLEAR gs_payment_bdcdata.
    IF sy-tabix NE 1.

* removing the double quotations
      PERFORM remove_double_quotations CHANGING gs_payment_data-lifnr.

      CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
        EXPORTING
          input  = gs_payment_data-lifnr
        IMPORTING
          output = gs_payment_bdcdata-lifnr.

      READ TABLE gt_vendor WITH
                      TABLE KEY lifnr = gs_payment_bdcdata-lifnr
                           INTO gs_vendor.
      IF sy-subrc NE 0.

        gs_vendor-lifnr = gs_payment_bdcdata-lifnr.

        INSERT gs_vendor INTO TABLE gt_vendor.
      ENDIF.

* removing the double quotations
      PERFORM remove_double_quotations CHANGING gs_payment_data-xblnr.
      gs_payment_bdcdata-xblnr  = gs_payment_data-xblnr.
      CONCATENATE sy-datum+4(2)
                  c_farw_slash
                  sy-datum+6(2)
                  c_farw_slash
                  sy-datum+2(2)
             INTO gs_payment_bdcdata-budat.

* changing the date format mm/dd/yyyy into user specific
      PERFORM date_format_userspec CHANGING gs_payment_bdcdata-budat.
* removing the double quotations
      PERFORM remove_double_quotations CHANGING gs_payment_data-waers.
      gs_payment_bdcdata-waers  = gs_payment_data-waers.
      gs_payment_bdcdata-blart  = 'KZ'.
      gs_payment_bdcdata-valut  = gs_payment_data-valut.
* changing the date format mm/dd/yyyy to into user specific
      PERFORM date_format_userspec CHANGING gs_payment_bdcdata-valut.
* removing the double quotations
      PERFORM remove_double_quotations CHANGING gs_payment_data-bktxt.
      gs_payment_bdcdata-bktxt  = gs_payment_data-bktxt.
      gs_payment_bdcdata-wrbtr  = gs_payment_data-wrbtr.

      gv_tot_amt = gv_tot_amt + gs_payment_bdcdata-wrbtr.

      gs_payment_bdcdata-disc   = ( gs_payment_data-wrbtr / 100 ) *
                                    gs_payment_data-disc.

      gv_tot_dis = gv_tot_dis + gs_payment_bdcdata-disc.

      gs_payment_bdcdata-bldat  = gs_payment_data-bldat.
* changing the date format mm/dd/yyyy into user specific
      PERFORM date_format_userspec CHANGING gs_payment_bdcdata-bldat.
* removing the double quotations
      PERFORM remove_double_quotations CHANGING gs_payment_data-belnr.

      CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
        EXPORTING
          input  = gs_payment_data-belnr
        IMPORTING
          output = gs_payment_bdcdata-belnr.

* removing the double quotations
      PERFORM remove_double_quotations CHANGING gs_payment_data-invid.
      gs_payment_bdcdata-invid  = gs_payment_data-invid.

      gs_reference-lifnr = gs_payment_bdcdata-lifnr.
      gs_reference-hbank = gs_payment_data-hbank.

      gs_reference-invid = gs_payment_data-invid.
      gs_reference-disc = gs_payment_bdcdata-disc.

* removing the double quotations
      PERFORM remove_double_quotations CHANGING gs_payment_data-sgtxt.
      gs_payment_bdcdata-sgtxt  = gs_payment_data-sgtxt.
* removing the double quotations
      PERFORM remove_double_quotations CHANGING gs_payment_data-zuonr.
      gs_payment_bdcdata-zuonr  = gs_payment_data-zuonr.
* removing the double quotations
      PERFORM remove_double_quotations CHANGING gs_payment_data-doctyp.

      lv_doctype = gs_payment_data-doctyp.

      TRANSLATE lv_doctype TO UPPER CASE.

      IF lv_doctype CS 'CREDIT'.
        gs_payment_data-doctyp = 'C'.
      ELSE.
        gs_payment_data-doctyp = 'I'.
      ENDIF.

      CLEAR lv_doctype.

      gs_payment_bdcdata-doctyp = gs_payment_data-doctyp.

      gs_reference-doctyp = gs_payment_bdcdata-doctyp.

* removing the double quotations
      PERFORM remove_double_quotations CHANGING gs_payment_data-hbank.

      gs_payment_bdcdata-hbank = gs_payment_data-hbank.

      APPEND gs_payment_bdcdata TO gt_payment_bdcdata.
      APPEND gs_reference TO gt_reference.

    ENDIF.                             " IF SY-TABIX NE 1

  ENDLOOP.                             " LOOP AT GT_PAYMENT_DATA INTO ..

  SELECT lifnr                         " Vendor Number
         bukrs                         " Comapany Code
    FROM lfb1
    INTO TABLE gt_company_codes
    FOR ALL ENTRIES IN gt_payment_bdcdata
   WHERE lifnr EQ gt_payment_bdcdata-lifnr.

  IF gt_company_codes IS INITIAL.

* filling the process details for extract log
    PERFORM fill_process_details USING text-ncf.

    gv_status = text-fal.
* LOG file on Application Server.
    PERFORM create_appserver_log.
    MESSAGE e025.

  ENDIF.                               " IF GT_COMAPNY_CODES IS INITIAL.

  SET LOCALE LANGUAGE sy-langu.        " for Translate key word in loop

  LOOP AT gt_payment_bdcdata INTO gs_payment_bdcdata.

    READ TABLE gt_payment_bdcheader
          INTO gs_payment_bdcheader
      WITH KEY lifnr = gs_payment_bdcdata-lifnr
               hbank = gs_payment_bdcdata-hbank.

    IF sy-subrc NE 0.

      gs_payment_bdcheader-lifnr = gs_payment_bdcdata-lifnr.

      READ TABLE gt_company_codes
            INTO gs_company_codes
        WITH KEY lifnr = gs_payment_bdcdata-lifnr.

      gs_payment_bdcheader-bukrs = gs_company_codes-bukrs.
      gs_payment_bdcheader-xblnr = gs_payment_bdcdata-xblnr.
      gs_payment_bdcheader-budat = gs_payment_bdcdata-budat.
      gs_payment_bdcheader-waers = gs_payment_bdcdata-waers.
      gs_payment_bdcheader-blart = gs_payment_bdcdata-blart.
      gs_payment_bdcheader-valut = gs_payment_bdcdata-valut.
      gs_payment_bdcheader-bktxt = gs_payment_bdcdata-bktxt.
      gs_payment_bdcheader-wrbtr = gs_payment_bdcdata-wrbtr.
      gs_payment_bdcheader-bldat = gs_payment_bdcdata-bldat.
      gs_payment_bdcheader-sgtxt = gs_payment_bdcdata-sgtxt.
      gs_payment_bdcheader-zuonr = gs_payment_bdcdata-zuonr.
      gs_payment_bdcheader-hbank = gs_payment_bdcdata-hbank.

      SPLIT gs_payment_bdcdata-hbank AT space
                                   INTO lv_hbkid
                                        lv_hktid.
      SELECT SINGLE
             land1
        INTO lv_country
        FROM t001
       WHERE bukrs = gs_company_codes-bukrs.

      SELECT zlsch
             text1
  INTO TABLE gt_pmt_methods
        FROM t042z
       WHERE land1 = lv_country.

      IF gt_pmt_methods IS INITIAL.
* filling the process details for extract log
        PERFORM fill_process_details USING text-npd.
        gv_status = text-fal.

* LOG file on Application Server.
        PERFORM create_appserver_log.

        MESSAGE e031.
      ELSE.

        LOOP AT gt_pmt_methods INTO gs_pmt_methods.
          lv_mtd_name = gs_pmt_methods-text1.
          TRANSLATE lv_mtd_name TO UPPER CASE.
          IF lv_mtd_name CS c_cheque OR lv_mtd_name CS c_check.
            lv_pmt_mtd = gs_pmt_methods-zlsch.
            EXIT.
          ENDIF.                       " IF LV_MTD_NAME CS C_CHEQUE ...
        ENDLOOP.                       " LOOP AT GT_PMT_METHODS INTO ...
        IF lv_pmt_mtd IS INITIAL.
* filling the process details for extract log
          PERFORM fill_process_details USING text-pcc.

          READ TABLE gt_pmt_methods INTO gs_pmt_methods INDEX 1.

          lv_pmt_mtd = gs_pmt_methods-zlsch.

          lv_string = text-pcp.
          REPLACE ALL OCCURRENCES OF c_p1
                                  IN lv_string
                                WITH lv_pmt_mtd.
          REPLACE ALL OCCURRENCES OF c_p2
                                  IN lv_string
                                WITH gs_payment_bdcheader-lifnr.

* filling the process details for extract log
          PERFORM fill_process_details USING lv_string.

        ENDIF.                         " IF LV_PMT_MTD IS INITIAL.
      ENDIF.                           " IF GT_PMT_METHODS IS INITIAL.

      SELECT SINGLE
             ukont                     " G/L Account to be Posted to
        INTO gs_payment_bdcheader-ukont
        FROM t042i
       WHERE zbukr EQ gs_company_codes-bukrs
         AND hbkid EQ lv_hbkid
         AND hktid EQ lv_hktid
         AND zlsch EQ lv_pmt_mtd
         AND waers EQ gs_payment_bdcdata-waers.

      IF sy-subrc NE 0.

        SELECT SINGLE
               ukont                   " G/L Account to be Posted to
          INTO gs_payment_bdcheader-ukont
          FROM t042i
         WHERE zbukr EQ gs_company_codes-bukrs
           AND hbkid EQ lv_hbkid
           AND hktid EQ lv_hktid
           AND zlsch EQ lv_pmt_mtd
           AND waers EQ space.

      ENDIF.                           " IF SY-SUBRC NE 0

      APPEND gs_payment_bdcheader TO gt_payment_bdcheader.

      CLEAR:
        lv_country,
        lv_pmt_mtd,
        lv_mtd_name,
        lv_string,
        gs_payment_bdcheader,
        gs_pmt_methods.

      REFRESH gt_pmt_methods.

    ENDIF.                             " IF SY-SUBRC NE 0

    IF gs_payment_bdcdata-belnr IS NOT INITIAL.

      lv_gjahr = sy-datum+0(4).

      SELECT SINGLE
             belnr                     " Accounting Document Number
        FROM bkpf
        INTO lv_belnr
       WHERE bukrs = gs_company_codes-bukrs
         AND belnr = gs_payment_bdcdata-belnr
         AND gjahr = lv_gjahr.

      IF sy-subrc EQ 0.

        gs_invoice-lifnr = gs_payment_bdcdata-lifnr.
        gs_invoice-hbank = gs_payment_bdcdata-hbank.
        gs_invoice-belnr = gs_payment_bdcdata-belnr.
        gs_invoice-disc = gs_payment_bdcdata-disc.

        APPEND gs_invoice TO gt_invoice.

        CLEAR gs_invoice.

      ENDIF.                           " IF SY-SUBRC EQ 0

    ENDIF.                             " IF BELNR IS NOT INITIAL

    CLEAR gs_payment_bdcdata.

  ENDLOOP.                             " LOOP AT GT_PAYMENT_BDCDATA ...

  LOOP AT gt_payment_bdcheader INTO gs_payment_bdcheader.

    CLEAR lv_wrbtr.

    lv_index = sy-tabix.

    LOOP AT gt_payment_bdcdata INTO gs_payment_bdcdata
      WHERE lifnr EQ gs_payment_bdcheader-lifnr
        AND hbank EQ gs_payment_bdcheader-hbank.

      IF gs_payment_bdcdata-doctyp EQ c_inv_doctyp.

        lv_wrbtr = lv_wrbtr
                   + gs_payment_bdcdata-wrbtr
                   - gs_payment_bdcdata-disc.

      ELSEIF gs_payment_bdcdata-doctyp = c_cmemo_doctyp.

        READ TABLE gt_reference INTO gs_reference
                            WITH KEY lifnr = gs_payment_bdcdata-lifnr
                                     hbank = gs_payment_bdcdata-hbank
                                     invid = gs_payment_bdcdata-invid
                                     doctyp = c_inv_doctyp.

        IF sy-subrc EQ 0.
          lv_wrbtr = lv_wrbtr - gs_payment_bdcdata-wrbtr.
        ENDIF.                         " IF SY-SUBRC EQ 0.

      ENDIF.                           " IF DOCTYP EQ C_INV_DOCTYP

    ENDLOOP.                           " LOOP AT GT_PAYMENT_BDCDATA ...

    gs_payment_bdcheader-wrbtr = lv_wrbtr.

    MODIFY gt_payment_bdcheader FROM gs_payment_bdcheader
                               INDEX lv_index.

  ENDLOOP.                             " LOOP AT GT_PAYMENT_BDCHEADER...

ENDFORM.                               " FORMAT_DATA

*----------------------------------------------------------------------*
*  Form  REMOVE_DOUBLE_QUOTATIONS                                      *
*----------------------------------------------------------------------*
*  This subroutine is for removing the double quotations marks.        *
*----------------------------------------------------------------------*
*  <-- cv_string  -  data filed                                        *
*----------------------------------------------------------------------*
FORM remove_double_quotations  CHANGING cv_string TYPE any.
  REPLACE ALL OCCURRENCES OF '"'
                          IN cv_string
                        WITH ''.
ENDFORM.                               " REMOVE_DOUBLE_QUOTATIONS

*----------------------------------------------------------------------*
*  Form  DATE_FORMAT_USERSPEC                                          *
*----------------------------------------------------------------------*
*  This Subroutine is for changing the date format into user specific  *
*----------------------------------------------------------------------*
*  <-- cv_date  -  Date                                                *
*----------------------------------------------------------------------*
FORM date_format_userspec  CHANGING cv_cdate TYPE char10.

  data: lv_i type i VALUE 1,
        lv_c type c,
        lv_off type i VALUE 0,
        lv_set type i,
        lv_nex type i,
        lv_day(2) type n,
        lv_month(2) type n,
        lv_year(4) type n,
        lv_idate type sy-datum.

  DO.
    lv_c = cv_cdate+lv_nex(1).
    IF lv_c eq '/'.
      IF lv_month is INITIAL.
        lv_month = cv_cdate+0(lv_set).
        lv_off = lv_i.
        CLEAR lv_set.
      ELSEIF lv_day is INITIAL.
        lv_day = cv_cdate+lv_off(lv_set).
        lv_year = cv_cdate+lv_i(2).
        lv_year = lv_year + 2000.
        exit.
      ENDIF.
    ELSE.
      lv_set = lv_set + 1.
    ENDIF.                             " IF LV_C EQ '/'.
    lv_i = lv_i + 1.
    lv_nex = lv_nex + 1.
  ENDDO.

  CONCATENATE lv_year
              lv_month
              lv_day
         into lv_idate.

    CALL 'DATE_CONV_INT_TO_EXT'
        ID 'DATINT' FIELD lv_idate
        ID 'DATEXT' FIELD cv_cdate.

ENDFORM.                               " DATE_FORMAT_USERSPEC

*----------------------------------------------------------------------*
*  Form  EXTRACT_STARTUP                                               *
*----------------------------------------------------------------------*
*  This subroutine is for getting the time stamp                       *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM extract_startup .
  GET TIME STAMP FIELD gv_timestamp.
  CONVERT TIME STAMP gv_timestamp TIME ZONE sy-zonlo
                INTO DATE gv_ts_date TIME gv_ts_time.
  gv_appser_log = text-alf.
  gv_er_filename = text-erf.
  gv_lg_filename = text-ilf.
ENDFORM.                               " EXTRACT_STARTUP

*----------------------------------------------------------------------*
*  Form  CREATE_APPSERVER_LOG                                          *
*----------------------------------------------------------------------*
*  This subroutine is for creating appserver log file                  *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the            *
*  subroutine.                                                         *
*----------------------------------------------------------------------*
FORM create_appserver_log.

* Local work variables..................................................
  DATA: lv_tab_string TYPE string.     " process details number


  OPEN DATASET gv_appser_log
           FOR APPENDING IN TEXT MODE
      ENCODING DEFAULT.

  IF sy-subrc NE 0.
* filling the process details for extract log
    PERFORM fill_process_details USING text-una.
    gv_status = text-fal.

* LOG file on Application Server.
    PERFORM create_appserver_log.
    MESSAGE e024.
  ENDIF.                               " IF SY-SUBRC NE 0.

  TRANSFER text-l01 TO gv_appser_log.
  TRANSFER text-l21 TO gv_appser_log.
  TRANSFER text-l01 TO gv_appser_log.
  TRANSFER text-l03 TO gv_appser_log.
  TRANSFER text-l04 TO gv_appser_log.

* line formating in server extract log file
  PERFORM dynamic_concat_*_appser USING text-l22
                                        gv_current_date.

* line formating in server extract log file
  PERFORM dynamic_concat_*_appser USING text-l23
                                        gv_current_time.
* line formating in server extract log file
  PERFORM dynamic_concat_*_appser USING text-l24
                                        sy-uname.
* line formating in server extract log file
  PERFORM dynamic_concat_*_appser USING text-l09
                                        sy-mandt.

  TRANSFER text-l01 TO gv_appser_log.
  TRANSFER text-l04 TO gv_appser_log.
* line formating in server extract log file
  PERFORM dynamic_concat_*_appser USING text-l19
                                        gv_status.
  TRANSFER text-l04 TO gv_appser_log.
  TRANSFER text-l18 TO gv_appser_log.

  LOOP AT gt_process_details INTO gs_process_details.

    lv_tab_string = sy-tabix.

    CONCATENATE c_star
                lv_tab_string
           INTO lv_tab_string SEPARATED BY space.
* line formating in server extract log file
    PERFORM dynamic_concat_*_appser USING lv_tab_string
                                          gs_process_details-tab_line.

  ENDLOOP.                             " LOOP AT GT_PROCESS_DETAILS ...

  TRANSFER text-l04 TO gv_appser_log.
  TRANSFER text-l20 TO gv_appser_log.
  TRANSFER text-l01 TO gv_appser_log.

  CLOSE DATASET gv_appser_log.
ENDFORM.                               " CREATE_APPSERVER_LOG

*----------------------------------------------------------------------*
*  Form  DATE_FORMAT_MMDDYYYY                                          *
*----------------------------------------------------------------------*
*  this subroutine for converting the date yyyymmdd to mm/dd/yyyy      *
*----------------------------------------------------------------------*
*  <-- cv_yyyymmdd_date  -  text                                       *
*----------------------------------------------------------------------*
FORM date_format_mmddyyyy  CHANGING cv_yyyymmdd_date TYPE any.

  CONCATENATE cv_yyyymmdd_date+4(2)
              c_farw_slash
              cv_yyyymmdd_date+6(2)
              c_farw_slash
              cv_yyyymmdd_date+0(4)
         INTO cv_yyyymmdd_date.

ENDFORM.                               " DATE_FORMAT_MMDDYYYY

*----------------------------------------------------------------------*
*  Form  DYNAMIC_CONCAT_*_APPSER                                       *
*----------------------------------------------------------------------*
*  This subroutine is for formatting the line in appserver extract log *
*----------------------------------------------------------------------*
*  --> iv_text    - text                                               *
*  --> iv_string  - text                                               *
*----------------------------------------------------------------------*
FORM dynamic_concat_*_appser  USING iv_text TYPE any
                                  iv_string TYPE any.

* Local work variables..................................................
  DATA:
    lv_string TYPE string,               " String
    lv_post_string TYPE string,          " String
    lv_pre_string TYPE string,           " String
    lv_line TYPE string,                 " String
    lv_line_len TYPE i,                  " Line length
    lv_pre_remain TYPE i,                " Pre string remain length
    lv_text_length TYPE i,               " Text length
    lv_string_length TYPE i,             " String length
    lv_write_length TYPE i,              " String line length
    lv_offset TYPE i,                    " Off set
    lv_remain_len TYPE i.                " Remain string length

  CONCATENATE iv_text
              space
         INTO lv_post_string.
  lv_text_length = STRLEN( lv_post_string ).
  lv_write_length = 71 - lv_text_length.
  lv_write_length = lv_write_length - 2.
  lv_string_length = STRLEN( iv_string ).
  lv_remain_len = lv_string_length.
  lv_offset = 0.
  WHILE lv_remain_len GT 0.
    IF lv_remain_len LT lv_write_length.
      lv_write_length = lv_remain_len.
    ENDIF.                             " IF LV_REMAIN_LEN LT LV_WR...
    lv_string = iv_string+lv_offset(lv_write_length).
    CONCATENATE lv_post_string
                space
                lv_string
           INTO lv_line.
    lv_line_len = STRLEN( lv_line ).
    lv_pre_string = text-l04.
    lv_pre_remain = 71 - lv_line_len.
    lv_pre_string = lv_pre_string+lv_line_len(lv_pre_remain).
    CONCATENATE lv_line
                lv_pre_string
           INTO lv_line.
    TRANSFER lv_line TO gv_appser_log.
    lv_post_string = text-l04.
    lv_post_string = lv_post_string+0(lv_text_length).
    lv_offset = lv_offset + lv_write_length.
    lv_remain_len = lv_string_length - lv_offset.
    CLEAR lv_line.
  ENDWHILE.                            " WHILE LV_REMAIN_LEN GT 0.
ENDFORM.                               " DYNAMIC_CONCAT_*_APPSER

*----------------------------------------------------------------------*
*  Form  DYNAMIC_CONCAT_*_LOG                                          *
*----------------------------------------------------------------------*
*  This subroutine is for formatting the line in appserver extract log *
*----------------------------------------------------------------------*
*  --> iv_text    - text                                               *
*  --> iv_string  - text                                               *
*----------------------------------------------------------------------*
FORM dynamic_concat_*_log  USING iv_text TYPE any
                                 iv_string TYPE any.

* Local work variables..................................................
  DATA:
    lv_string TYPE string,               " String
    lv_post_string TYPE string,          " String
    lv_pre_string TYPE string,           " String
    lv_line TYPE string,                 " String
    lv_line_len TYPE i,                  " Line length
    lv_pre_remain TYPE i,                " Pre string remain length
    lv_text_length TYPE i,               " Text length
    lv_string_length TYPE i,             " String length
    lv_write_length TYPE i,              " String line length
    lv_offset TYPE i,                    " Off set
    lv_remain_len TYPE i.                " Remain string length

  CONCATENATE iv_text
              space
         INTO lv_post_string.
  lv_text_length = STRLEN( lv_post_string ).
  lv_write_length = 71 - lv_text_length.
  lv_write_length = lv_write_length - 2.
  lv_string_length = STRLEN( iv_string ).
  lv_remain_len = lv_string_length.
  lv_offset = 0.
  WHILE lv_remain_len GT 0.
    IF lv_remain_len LT lv_write_length.
      lv_write_length = lv_remain_len.
    ENDIF.                             " IF LV_REMAIN_LEN LT LV_WR...
    lv_string = iv_string+lv_offset(lv_write_length).
    CONCATENATE lv_post_string
                space
                lv_string
           INTO lv_line.
    lv_line_len = STRLEN( lv_line ).
    lv_pre_string = text-l04.
    lv_pre_remain = 71 - lv_line_len.
    lv_pre_string = lv_pre_string+lv_line_len(lv_pre_remain).
    CONCATENATE lv_line
                lv_pre_string
           INTO lv_line.
    TRANSFER lv_line TO gv_lg_fullpath.
    lv_post_string = text-l04.
    lv_post_string = lv_post_string+0(lv_text_length).
    lv_offset = lv_offset + lv_write_length.
    lv_remain_len = lv_string_length - lv_offset.
    CLEAR lv_line.
  ENDWHILE.                            " WHILE LV_REMAIN_LEN GT 0.
ENDFORM.                               " DYNAMIC_CONCAT_*_LOG

*----------------------------------------------------------------------*
*  Form  FILL_PROCESS_DETAILS                                          *
*----------------------------------------------------------------------*
*  This subroutine is for filling the process details tab              *
*----------------------------------------------------------------------*
*  --> iv_text  -  text row for process details table                  *
*----------------------------------------------------------------------*
FORM fill_process_details  USING iv_text TYPE any.

  APPEND iv_text TO gt_process_details.

ENDFORM.                               " FILL_PROCESS_DETAILS

*----------------------------------------------------------------------*
*  Form  OPEN_SESSION                                                  *
*----------------------------------------------------------------------*
*  This subroutine is to open bdc session for failed transanctions     *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM open_session .

* Local work variables..................................................
  DATA: lv_name TYPE char12,           " Session name
        lv_string TYPE string.         " String

  CONCATENATE gv_ts_date+2(6)
              gv_ts_time
         INTO lv_name.

  CALL FUNCTION 'BDC_OPEN_GROUP'
    EXPORTING
      client              = sy-mandt
      group               = lv_name
      user                = sy-uname
      keep                = 'X'
    EXCEPTIONS
      client_invalid      = 1
      destination_invalid = 2
      group_invalid       = 3
      group_is_locked     = 4
      holddate_invalid    = 5
      internal_error      = 6
      queue_error         = 7
      running             = 8
      system_lock_error   = 9
      user_invalid        = 10
      OTHERS              = 11.

  IF sy-subrc EQ 0.
* filling the process details for extract log
    PERFORM fill_process_details USING text-eop.

    MESSAGE s015 WITH lv_name.
    lv_string = text-sgn.
    REPLACE ALL OCCURRENCES OF c_p1
                            IN lv_string
                          WITH lv_name.
* filling the process details for extract log
    PERFORM fill_process_details USING lv_string.

    gv_status = lv_string.
  ENDIF.                               " IF SY-SUBRC EQ 0.

ENDFORM.                               " OPEN_SESSION

*----------------------------------------------------------------------*
*  Form  CREATE_SESSION_ERECORDS                                       *
*----------------------------------------------------------------------*
*  This subroutine is to creating bdc session for failed transanctions *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM create_session_erecords .

* creating open session method for failed transactions.
  PERFORM open_session.

* filling bdc data for session method for failed transactions
  PERFORM bdc_data_session.

* closing the session group
  PERFORM close_session.
  gv_status = text-fal.

ENDFORM.                               " CREATE_SESSION_ERECORDS

*----------------------------------------------------------------------*
*  Form  BDC_DATA_SESSION                                              *
*----------------------------------------------------------------------*
*  This subroutine is to fill the bdc data for session method          *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM bdc_data_session .

* Local work variables..................................................
  DATA: lv_fname TYPE string,          " String
        lv_invid TYPE rf05a-sel01,     " Invoice ID
        lv_hbank TYPE char13,          " House bank
        lv_lifnr TYPE lfb1-lifnr,      " Vendor Number
        lv_row TYPE char2.             " Row number

* Local table declarations..............................................
  DATA:
        lt_uniref TYPE SORTED TABLE
                    OF typ_uniref
       WITH UNIQUE KEY invid
          INITIAL SIZE 0.

  DATA:
        lt_uniinv TYPE STANDARD TABLE
                    OF typ_uniinv
          INITIAL SIZE 0.

  DATA:
        lt_refdis TYPE STANDARD TABLE
                    OF typ_dis
          INITIAL SIZE 0.

  DATA:
        lt_invdis TYPE STANDARD TABLE
                    OF typ_dis
          INITIAL SIZE 0.

* Local field string declarations.......................................
  DATA:
    ls_uniref TYPE typ_uniref.

  DATA:
    ls_refdis TYPE typ_dis.

  DATA:
    ls_uniinv TYPE typ_uniinv.

  DATA:
    ls_invdis TYPE typ_dis.

  LOOP AT gt_error_data INTO gs_error_data.

    REFRESH gt_bdc_data.

    PERFORM bdc_dynpro      USING 'SAPMF05A'
                                  '0103'.
    PERFORM bdc_field       USING 'BDC_CURSOR'
                                  'RF05A-XPOS1(06)'.
    PERFORM bdc_field       USING 'BDC_OKCODE'
                                  '/00'.
    PERFORM bdc_field       USING 'BKPF-BLDAT'
                                  gs_error_data-bldat.
    " Adjusted Payment Date

    PERFORM bdc_field       USING 'BKPF-BLART'
                                  gs_error_data-blart.
    " Document type

    PERFORM bdc_field       USING 'BKPF-BUKRS'
                                  gs_error_data-bukrs.
    " Company code

    PERFORM bdc_field       USING 'BKPF-BUDAT'
                                  gs_error_data-budat.
    " Posting date

    PERFORM bdc_field       USING 'BKPF-MONAT'
                                  ''.  " Posting period

    PERFORM bdc_field       USING 'BKPF-WAERS'
                                  gs_error_data-waers.
    " Currency key

    PERFORM bdc_field       USING 'BKPF-XBLNR'
                                  gs_error_data-xblnr.
    " Pollenware Vendor ID

    PERFORM bdc_field       USING 'BKPF-BKTXT'
                                  gs_error_data-bktxt.
    " Event ID

    PERFORM bdc_field       USING 'RF05A-KONTO'
                                  gs_error_data-ukont.
    " Account

    PERFORM bdc_field       USING 'BSEG-WRBTR'
                                  gs_error_data-wrbtr.
    " Amount

    PERFORM bdc_field       USING 'BSEG-VALUT'
                                  gs_error_data-valut.
    " Original Invoice Due Date

    PERFORM bdc_field       USING 'BSEG-SGTXT'
                                  gs_error_data-sgtxt.
    " Item text

    PERFORM bdc_field       USING 'BSEG-ZUONR'
                                  gs_error_data-zuonr.
    " Assignment

    PERFORM bdc_field       USING 'RF05A-AGKON'
                                  gs_error_data-lifnr.
    " Vendor Account

    PERFORM bdc_field       USING 'RF05A-AGKOA'
                                  'K'.
    PERFORM bdc_field       USING 'RF05A-XNOPS'
                                  'X'.

    READ TABLE gt_invoice INTO gs_invoice
      WITH KEY lifnr = gs_error_data-lifnr
               hbank = gs_error_data-hbank.

    IF sy-subrc EQ 0.

      PERFORM bdc_field       USING 'RF05A-XPOS1(01)'
                                    ''.
      PERFORM bdc_field       USING 'RF05A-XPOS1(03)'
                                    'X'.

      PERFORM bdc_dynpro      USING 'SAPMF05A'
                                    '0731'.
      PERFORM bdc_field       USING 'BDC_CURSOR'
                                    'RF05A-SEL01(01)'.
      PERFORM bdc_field       USING 'BDC_OKCODE'
                                    '=PA'.

      LOOP AT gt_invoice INTO gs_invoice
                        WHERE lifnr = gs_error_data-lifnr
                          AND hbank = gs_error_data-hbank.

        CLEAR ls_uniinv.

        READ TABLE lt_uniinv INTO ls_uniinv
                   WITH TABLE KEY belnr = gs_invoice-belnr.

        IF sy-subrc NE 0.
          ls_uniinv-belnr = gs_invoice-belnr.
          APPEND ls_uniinv TO lt_uniinv.
        ENDIF.                         " IF SY-SUBRC NE 0

      ENDLOOP.                         " LOOP AT GT_INVOICE INTO GS_I...

      LOOP AT lt_uniinv INTO ls_uniinv.

        CLEAR: lv_fname,
               lv_row.

        IF sy-tabix < 10.
          lv_row = sy-tabix.
          CONCATENATE 'RF05A-SEL01(0'
                      lv_row
                      ')'
                 INTO lv_fname.
        ELSE.
          lv_row = sy-tabix.
          CONCATENATE 'RF05A-SEL01('
                      lv_row
                      ')'
                 INTO lv_fname.
        ENDIF.                         " IF SY-TABIX < 10.

        PERFORM bdc_field USING lv_fname
                                ls_uniinv-belnr.

      ENDLOOP.                         " LOOP AT LT_UNIINV INTO LS_UN...

      PERFORM bdc_dynpro      USING 'SAPDF05X' '3100'.

      PERFORM bdc_field       USING 'BDC_OKCODE'
                                    '/00'.

      PERFORM bdc_field       USING 'BDC_CURSOR'
                                    'DF05B-PSSKT(01)'.
      PERFORM bdc_field       USING 'RF05A-ABPOS'
                                    '1'.

      LOOP AT gt_invoice INTO gs_invoice
                        WHERE lifnr = gs_error_data-lifnr
                          AND hbank = gs_error_data-hbank.

        CLEAR ls_invdis.

        ls_invdis-disc = gs_invoice-disc.

        APPEND ls_invdis TO lt_invdis.

      ENDLOOP.                         " LOOP AT GT_INVOICE INTO GS_I...

      LOOP AT lt_invdis INTO ls_invdis.

        CLEAR: lv_fname,
               lv_row.

        IF sy-tabix < 10.
          lv_row = sy-tabix.
          CONCATENATE 'DF05B-PSSKT(0'
                      lv_row
                      ')'
                 INTO lv_fname.
        ELSE.
          lv_row = sy-tabix.
          CONCATENATE 'DF05B-PSSKT(0'
                      lv_row
                      ')'
                 INTO lv_fname.
        ENDIF.                         " IF SY-TABIX < 10

        PERFORM bdc_field USING lv_fname
                                ls_invdis-disc.

      ENDLOOP.                         " LOOP AT LT_INVDIS INTO GS_IN...

      PERFORM bdc_dynpro      USING 'SAPDF05X'
                                    '3100'.

      PERFORM bdc_field       USING 'BDC_OKCODE'
                                    '=BS'.
      PERFORM bdc_field       USING 'BDC_CURSOR'
                                    'DF05B-PSSKT(01)'.
      PERFORM bdc_field       USING 'RF05A-ABPOS'
                                    '1'.
      PERFORM bdc_dynpro      USING 'SAPMF05A'
                                    '0700'.
      PERFORM bdc_field       USING 'BDC_CURSOR'
                                    'RF05A-NEWBS'.
      PERFORM bdc_field       USING 'BDC_OKCODE'
                                    '=BU'.
      PERFORM bdc_field       USING 'BKPF-XBLNR'
                                    gs_error_data-xblnr.
      PERFORM bdc_field       USING 'BKPF-BKTXT'
                                    gs_error_data-bktxt.

    ELSE.

      PERFORM bdc_field       USING 'RF05A-XPOS1(01)'
                                    ''.
      PERFORM bdc_field       USING 'RF05A-XPOS1(06)'
                                    'X'.

      READ TABLE gt_payment_bdcdata
            INTO gs_payment_bdcdata
        WITH KEY lifnr = gs_error_data-lifnr
                 hbank = gs_error_data-hbank
                 doctyp = c_cmemo_doctyp.

      IF sy-subrc EQ 0.

        lv_lifnr = gs_payment_bdcdata-lifnr.
        lv_invid = gs_payment_bdcdata-invid.
        lv_hbank = gs_payment_bdcdata-hbank.

        READ TABLE gt_payment_bdcdata
              INTO gs_payment_bdcdata
          WITH KEY lifnr = lv_lifnr
                   invid = lv_invid
                   hbank = lv_hbank
                   doctyp = c_inv_doctyp .

        IF sy-subrc NE 0.

          PERFORM bdc_dynpro      USING 'SAPMF05A'
                                        '0608'.
          PERFORM bdc_field       USING 'BDC_CURSOR'
                                        'RF05A-XPOS1(05)'.
          PERFORM bdc_field       USING 'BDC_OKCODE'
                                        '=ENTR'.
          PERFORM bdc_field       USING 'RF05A-XPOS1(01)'
                                        ''.
          PERFORM bdc_field       USING 'RF05A-XPOS1(07)'
                                        'X'.
          PERFORM bdc_dynpro      USING 'SAPMF05A'
                                        '0731'.
          PERFORM bdc_field       USING 'BDC_CURSOR'
                                        'RF05A-SEL01(01)'.
          PERFORM bdc_field       USING 'BDC_OKCODE'
                                        '=SL2'.
          PERFORM bdc_field       USING 'RF05A-SEL01(01)'
                                        'KG'.
          PERFORM bdc_dynpro      USING 'SAPMF05A'
                                        '0608'.
          PERFORM bdc_field       USING 'BDC_CURSOR'
                                        'RF05A-XPOS1(05)'.
          PERFORM bdc_field       USING 'BDC_OKCODE'
                                        '=ENTR'.

        ELSE.

          PERFORM bdc_dynpro      USING 'SAPMF05A'
                                        '0608'.
          PERFORM bdc_field       USING 'BDC_CURSOR'
                                        'RF05A-XPOS1(06)'.
          PERFORM bdc_field       USING 'BDC_OKCODE'
                                        '=ENTR'.

        ENDIF.                         " IF SY-SUBRC NE 0.

      ENDIF.                           " IF SY-SUBRC EQ 0.

      PERFORM bdc_field       USING 'RF05A-XPOS1(01)'
                                    ''.
      PERFORM bdc_field       USING 'RF05A-XPOS1(05)'
                                    'X'.
      PERFORM bdc_dynpro      USING 'SAPMF05A'
                                    '0731'.
      PERFORM bdc_field       USING 'BDC_CURSOR'
                                    'RF05A-SEL01(01)'.
      PERFORM bdc_field       USING 'BDC_OKCODE'
                                    '=PA'.

      LOOP AT gt_reference INTO gs_reference
                          WHERE lifnr = gs_error_data-lifnr
                            AND hbank = gs_error_data-hbank.

        CLEAR ls_uniref.

        READ TABLE lt_uniref INTO ls_uniref
                   WITH TABLE KEY invid = gs_reference-invid.

        IF sy-subrc NE 0.
          ls_uniref-invid = gs_reference-invid.
          INSERT ls_uniref INTO TABLE lt_uniref.
        ENDIF.                         " IF SY-SUBRC NE 0

      ENDLOOP.                         " LOOP AT GT_REFERENCE INTO ...

      LOOP AT lt_uniref INTO ls_uniref.

        CLEAR: lv_fname,
               lv_row.

        IF sy-tabix < 10.
          lv_row = sy-tabix.
          CONCATENATE 'RF05A-SEL01(0'
                      lv_row
                      ')'
                 INTO lv_fname.
        ELSE.
          lv_row = sy-tabix.
          CONCATENATE 'RF05A-SEL01('
                      lv_row
                      ')'
                 INTO lv_fname.
        ENDIF.                         " IF SY-TABIX < 10

        PERFORM bdc_field       USING lv_fname
                                      ls_uniref-invid.
        " Invoice Id

      ENDLOOP.                         " LOOP AT LT_UNIREF INTO LS_...

      PERFORM bdc_dynpro      USING 'SAPDF05X'
                                    '3100'.
      PERFORM bdc_field       USING 'BDC_OKCODE'
                                    '/00'.
      PERFORM bdc_field       USING 'BDC_CURSOR'
                                    'DF05B-PSSKT(01)'.
      PERFORM bdc_field       USING 'RF05A-ABPOS'
                                    '1'.

      LOOP AT gt_reference INTO gs_reference
                          WHERE lifnr = gs_error_data-lifnr
                            AND hbank = gs_error_data-hbank.

        CLEAR ls_refdis.

        ls_refdis-disc = gs_reference-disc.

        IF gs_reference-doctyp EQ c_cmemo_doctyp.
          INSERT ls_refdis INTO lt_refdis INDEX 1.
        ELSE.
          APPEND ls_refdis TO lt_refdis.
        ENDIF.                         " IF DOCTYP EQ C_DMEMO_DOCTYP

      ENDLOOP.                         " LOOP AT GT_REFERENCE INTO ...


      LOOP AT lt_refdis INTO ls_refdis.

        CLEAR: lv_fname,
               lv_row.

        IF sy-tabix < 10.
          lv_row = sy-tabix.
          CONCATENATE 'DF05B-PSSKT(0'
                      lv_row
                      ')'
                 INTO lv_fname.
        ELSE.
          lv_row = sy-tabix.
          CONCATENATE 'DF05B-PSSKT(0'
                      lv_row
                      ')'
                 INTO lv_fname.
        ENDIF.                         " IF SY-TABIX < 10.

        PERFORM bdc_field USING lv_fname
                                ls_refdis-disc.

      ENDLOOP.                         " LOOP AT LT_REFDIS INTO LS_RE...

      PERFORM bdc_dynpro      USING 'SAPDF05X'
                                    '3100'.
      PERFORM bdc_field       USING 'BDC_OKCODE'
                                    '=BS'.
      PERFORM bdc_field       USING 'BDC_CURSOR'
                                    'DF05B-PSSKT(01)'.
      PERFORM bdc_field       USING 'RF05A-ABPOS'
                                    '1'.
      PERFORM bdc_dynpro      USING 'SAPMF05A'
                                    '0700'.
      PERFORM bdc_field       USING 'BDC_CURSOR'
                                    'RF05A-NEWBS'.
      PERFORM bdc_field       USING 'BDC_OKCODE'
                                    '=BU'.
      PERFORM bdc_field       USING 'BKPF-XBLNR'
                                    gs_error_data-xblnr.
      " Pollenware Vendor ID
      PERFORM bdc_field       USING 'BKPF-BKTXT'
                                    gs_error_data-bktxt.
      " Event ID

    ENDIF.                             " IF SY-SUBRC EQ 0.


    PERFORM bdc_transaction USING 'F-53'.

  ENDLOOP.                             " Loop at gt_error_data into ...

ENDFORM.                               " BDC_DATA_SESSION

*----------------------------------------------------------------------*
*  Form  BDC_TRANSACTION
*----------------------------------------------------------------------*
*  This subroutine is to call transaction for session method           *
*----------------------------------------------------------------------*
*  --> lv_tcode  -  t code                                             *
*----------------------------------------------------------------------*
FORM bdc_transaction  USING tcode TYPE tstc-tcode.
  CALL FUNCTION 'BDC_INSERT'
    EXPORTING
      tcode     = tcode
    TABLES
      dynprotab = gt_bdc_data.
ENDFORM.                               " BDC_TRANSACTION

*----------------------------------------------------------------------*
*  Form  CLOSE_SESSION                                                 *
*----------------------------------------------------------------------*
*  This subroutine is for close the session group
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM close_session .
  CALL FUNCTION 'BDC_CLOSE_GROUP'.
ENDFORM.                               " CLOSE_SESSION

*----------------------------------------------------------------------*
*  Form  SAVE_ERECORDS_APPSERVER                                       *
*----------------------------------------------------------------------*
*  This subroutine is to save failed records to application server     *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM save_erecords_appserver .

* filtering failed transactions
  PERFORM filter_erecords.

* converting failed transactions data to CSV format.
  PERFORM convert_data_csv.

* creating CSV flat files on app server
  PERFORM create_csv_flat_file.

ENDFORM.                               " SAVE_ERECORDS_APPSERVER

*----------------------------------------------------------------------*
*  Form  FILTER_ERECORDS                                               *
*----------------------------------------------------------------------*
*  This subroutine is to filter the failed records from total records  *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM filter_erecords .

  DATA lv_lifnr TYPE typ_payment_data-lifnr.

  CLEAR gs_success_bdc.

  LOOP AT gt_success_bdc INTO gs_success_bdc.

    CALL FUNCTION 'CONVERSION_EXIT_ALPHA_OUTPUT'
      EXPORTING
        input  = gs_success_bdc-lifnr
      IMPORTING
        output = lv_lifnr.

    DELETE gt_payment_data WHERE lifnr = lv_lifnr
                             AND hbank = gs_success_bdc-hbank.

    IF sy-subrc EQ 0.

      CONCATENATE c_double_quotation
                  lv_lifnr
                  c_double_quotation
             INTO lv_lifnr.

      CONCATENATE c_double_quotation
                  gs_success_bdc-hbank
                  c_double_quotation
             INTO gs_success_bdc-hbank.

      DELETE gt_payment_data WHERE lifnr = lv_lifnr
                               AND hbank = gs_success_bdc-hbank.

    ENDIF.

  ENDLOOP.

ENDFORM.                               " FILTER_ERECORDS

*----------------------------------------------------------------------*
*  Form  CONVERT_DATA_CSV                                              *
*----------------------------------------------------------------------*
*  This Subroutine is for converting internal table data to CSV format *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM convert_data_csv .

  CALL FUNCTION 'SAP_CONVERT_TO_TEX_FORMAT'
    EXPORTING
      i_field_seperator          = ','
*     I_LINE_HEADER              =
*     I_FILENAME                 =
*     I_APPL_KEEP                = ' '
    TABLES
      i_tab_sap_data             = gt_payment_data
    CHANGING
      i_tab_converted_data       = gt_erecords_csv.

ENDFORM.                               " CONVERT_DATA_CSV

*----------------------------------------------------------------------*
*  Form  CREATE_INBOUND_LOG                                            *
*----------------------------------------------------------------------*
*  Subroutine for creating CSV flat files on appserver                 *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM create_inbound_log.

  CONCATENATE gv_lg_filename
              gv_ts_date
              c_underscore
              gv_ts_time
              c_dot
              c_rtf_extension
         INTO gv_lg_filename.

* creating the log file to application server
  PERFORM create_log_file.

ENDFORM.                               " CREATE_INBOUND_LOG

*----------------------------------------------------------------------*
*  Form  CREATE_LOG_FILE                                               *
*----------------------------------------------------------------------*
*  This Subroutine is for creating the file to application server      *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM create_log_file .

* local work variables..................................................
  DATA:
    lv_length TYPE i,                  " Length
    lv_char TYPE char1,                " Character
    lv_slash TYPE char1,               " Slash type in path
    lv_total_records  TYPE i,          " Total records
    lv_total_erecords  TYPE i,         " Total error records
    lv_total_srecords  TYPE i,         " Total success records
    lv_string_totrec TYPE string,      " Total records in string
    lv_tab_string TYPE string.         " process details number


  IF p_lfpath IS NOT INITIAL.
    lv_length = STRLEN( p_lfpath ).
    lv_length = lv_length - 1.
    lv_char = p_lfpath+lv_length(1).

    IF p_lfpath CA c_farw_slash.
      lv_slash = c_farw_slash.
    ELSE.
      lv_slash = c_back_slash.
    ENDIF.                             " IF P_EFPATH CA C_FARW_SLASH.

    IF lv_char EQ lv_slash.
      CONCATENATE p_lfpath
                  gv_lg_filename
             INTO gv_lg_fullpath.
    ELSE.
      CONCATENATE p_lfpath
                  c_back_slash
                  gv_lg_filename
             INTO gv_lg_fullpath.
    ENDIF.                             " IF LV_CHAR EQ LV_SLASH.
  ELSE.
    gv_lg_fullpath = gv_lg_filename.
  ENDIF.                               " IF P_EFPATH IS NOT INITIAL.

  OPEN DATASET gv_lg_fullpath FOR OUTPUT IN TEXT MODE
                                ENCODING DEFAULT.
  IF sy-subrc EQ 0.

    TRANSFER text-l01 TO gv_lg_fullpath.
    TRANSFER text-l21 TO gv_lg_fullpath.
    TRANSFER text-l01 TO gv_lg_fullpath.
    TRANSFER text-l03 TO gv_lg_fullpath.
    TRANSFER text-l04 TO gv_lg_fullpath.

    gv_current_date = gv_ts_date.

* change date format YYYYMMDD into MM/DD/YYYY
    PERFORM date_format_mmddyyyy CHANGING gv_current_date.

* line formating in server extract log file
    PERFORM dynamic_concat_*_log USING text-l22
                                        gv_current_date.
    CONCATENATE gv_ts_time+0(2)
                c_colon
                gv_ts_time+2(2)
                c_colon
                gv_ts_time+4(2)
           INTO gv_current_time.

* line formating in server extract log file
    PERFORM dynamic_concat_*_log USING text-l23
                                     gv_current_time.
* line formating in server extract log file
    PERFORM dynamic_concat_*_log USING text-l24
                                     sy-uname.
* line formating in server extract log file
    PERFORM dynamic_concat_*_log USING text-l09
                                     sy-mandt.

    TRANSFER text-l04 TO gv_lg_fullpath.

    DESCRIBE TABLE gt_payment_bdcdata LINES lv_total_records.
    lv_string_totrec = lv_total_records.

* line formating in extract log file
    PERFORM dynamic_concat_*_log USING text-l16
                                       lv_string_totrec.

    DESCRIBE TABLE gt_payment_data LINES lv_total_erecords.
    lv_total_erecords = lv_total_erecords - 1.


    lv_total_srecords = lv_total_records - lv_total_erecords.
    lv_string_totrec = lv_total_srecords.

* line formating in extract log file
    PERFORM dynamic_concat_*_log USING text-l15
                                       lv_string_totrec.

    lv_string_totrec = lv_total_erecords.

* line formating in extract log file
    PERFORM dynamic_concat_*_log USING text-l14
                                       lv_string_totrec.

    CLEAR: lv_total_records,
           lv_string_totrec.

    DESCRIBE TABLE gt_vendor LINES lv_total_records.

    lv_string_totrec = lv_total_records.

* line formating in extract log file
    PERFORM dynamic_concat_*_log USING text-l13
                                       lv_string_totrec.

    CLEAR lv_string_totrec.

    lv_string_totrec = gv_tot_amt.

    CONCATENATE lv_string_totrec
                space
                c_usd
           INTO lv_string_totrec.

* line formating in extract log file
    PERFORM dynamic_concat_*_log USING text-l12
                                       lv_string_totrec.

    CLEAR lv_string_totrec.

    lv_string_totrec = gv_tot_dis.

    CONCATENATE lv_string_totrec
                space
                c_usd
           INTO lv_string_totrec.

* line formating in extract log file
    PERFORM dynamic_concat_*_log USING text-l11
                                       lv_string_totrec.

    TRANSFER text-l01 TO gv_lg_fullpath.
    TRANSFER text-l04 TO gv_lg_fullpath.
* line formating in server extract log file
    PERFORM dynamic_concat_*_log USING text-l19
                                     gv_status.
    TRANSFER text-l04 TO gv_lg_fullpath.
    TRANSFER text-l18 TO gv_lg_fullpath.

    LOOP AT gt_process_details INTO gs_process_details.

      lv_tab_string = sy-tabix.

      CONCATENATE c_star
                lv_tab_string
           INTO lv_tab_string SEPARATED BY space.
* line formating in server extract log file
      PERFORM dynamic_concat_*_log USING lv_tab_string
                                       gs_process_details-tab_line.

    ENDLOOP.                             " LOOP AT GT_PROCESS_DETAILS ...

    TRANSFER text-l04 TO gv_lg_fullpath.
    TRANSFER text-l20 TO gv_lg_fullpath.
    TRANSFER text-l01 TO gv_lg_fullpath.

    CLOSE DATASET gv_lg_fullpath.
  ENDIF.                               " IF SY-SUBRC EQ 0.

ENDFORM.                               " CREATE_LOG_FILE

*----------------------------------------------------------------------*
*  Form  CREATE_CSV_FLAT_FILE                                          *
*----------------------------------------------------------------------*
*  Subroutine for creating CSV flat files on appserver                 *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM create_csv_flat_file.

  CONCATENATE gv_er_filename
              gv_ts_date
              c_underscore
              gv_ts_time
              c_dot
              c_csv_extension
         INTO gv_er_filename.

* creating the file to application server
  PERFORM create_file_appserver.

ENDFORM.                               " CREATE_CSV_FLAT_FILE

*----------------------------------------------------------------------*
*  Form  CREATE_FILE_APPSERVER                                         *
*----------------------------------------------------------------------*
*  This Subroutine is for creating the file to application server      *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM create_file_appserver .

* local work variables..................................................
  DATA:
    lv_length TYPE i,                  " Length
    lv_char TYPE char1,                " Character
    lv_slash TYPE char1.               " Slash type in path

  IF p_efpath IS NOT INITIAL.
    lv_length = STRLEN( p_efpath ).
    lv_length = lv_length - 1.
    lv_char = p_efpath+lv_length(1).

    IF p_efpath CA c_farw_slash.
      lv_slash = c_farw_slash.
    ELSE.
      lv_slash = c_back_slash.
    ENDIF.                             " IF P_EFPATH CA C_FARW_SLASH.

    IF lv_char EQ lv_slash.
      CONCATENATE p_efpath
                  gv_er_filename
             INTO gv_er_fullpath.
    ELSE.
      CONCATENATE p_efpath
                  c_back_slash
                  gv_er_filename
             INTO gv_er_fullpath.
    ENDIF.                             " IF LV_CHAR EQ LV_SLASH.
  ELSE.
    gv_er_fullpath = gv_er_filename.
  ENDIF.                               " IF P_EFPATH IS NOT INITIAL.

  OPEN DATASET gv_er_fullpath FOR OUTPUT IN TEXT MODE
                                ENCODING DEFAULT.
  IF sy-subrc EQ 0.

    LOOP AT gt_erecords_csv INTO gs_erecords_csv.
      TRANSFER gs_erecords_csv TO gv_er_fullpath.
    ENDLOOP.                           " LOOP AT GT_ERECORDS INTO ...

    CLOSE DATASET gv_er_fullpath.
  ENDIF.                               " IF SY-SUBRC EQ 0.

ENDFORM.                               " CREATE_FILE_APPSERVER

*----------------------------------------------------------------------*
*  Form  FETCH_CONFIG_DETAILS                                          *
*----------------------------------------------------------------------*
*  This subroutine is for fetching quickpay configuration details      *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM fetch_config_details .

* Local work variables..................................................

  DATA: lv_subrc TYPE sy-subrc.        " sy-subrc
*        lv_length  TYPE i.             " string length

  REFRESH gt_config_file.

  IF gv_config_file IS INITIAL.

    CONCATENATE sy-mandt
                c_configure
                c_dot
                c_csv_extension
           INTO gv_config_file.
  ENDIF.                               " IF GV_CONFIG_FILE IS INITIAL

  OPEN DATASET gv_config_file FOR INPUT IN TEXT MODE ENCODING DEFAULT.

  IF sy-subrc NE 0.
* filling the process details for extract log
    PERFORM fill_process_details USING text-uaa.
    gv_status = text-fal.

* LOG file on Application Server.
    PERFORM create_appserver_log.
    MESSAGE e030.
  ENDIF.                               " IF SY-SUBRC NE 0

  DO.
    READ DATASET gv_config_file INTO gs_config_csv.
    lv_subrc = sy-subrc.
    IF lv_subrc NE 0.
      EXIT.
    ENDIF.                             " IF SY-SUBRC NE 0
    SPLIT gs_config_csv AT c_comma
                      INTO gs_config_file-mode
                           gs_config_file-type.

    APPEND gs_config_file TO gt_config_file.
    CLEAR gs_config_file.
  ENDDO.                               " IF SY-SUBRC NE 0 EXIT

  CLEAR lv_subrc.

  CLOSE DATASET gv_config_file.

  IF gv_config_details IS INITIAL.
    CONCATENATE sy-mandt
                c_config_details
                c_dot
                c_csv_extension
           INTO gv_config_details.
  ENDIF.                               " IF GV_CONFIG_DETAILS IS INITIAL

  OPEN DATASET gv_config_details FOR INPUT IN TEXT MODE
                                     ENCODING DEFAULT.

  IF sy-subrc NE 0.
* filling the process details for extract log
    PERFORM fill_process_details USING text-uaa.
    gv_status = text-fal.

* LOG file on Application Server.
    PERFORM create_appserver_log.

    MESSAGE e030.
  ENDIF.                               " IF SY-SUBRC NE 0

  DO.
    READ DATASET gv_config_details INTO gs_config_csv.
    lv_subrc = sy-subrc.
    IF lv_subrc NE 0.
      EXIT.
    ENDIF.                             " IF SY-SUBRC NE 0
    SPLIT gs_config_csv AT c_comma
                      INTO gs_config_details-mode
                           gs_config_details-type
                           gs_config_details-value.

    APPEND gs_config_details TO gt_config_details.
    CLEAR gs_config_details.
  ENDDO.                               " IF SY-SUBRC NE 0 EXIT

  CLOSE DATASET gv_config_details.

  READ TABLE gt_config_file INTO gs_config_file
                          WITH KEY mode = c_inbound.
  IF sy-subrc EQ 0.
    IF gs_config_file-type EQ c_app_server.
      gv_as = c_flag_on.
      READ TABLE gt_config_details INTO gs_config_details
                               WITH KEY mode = c_inbound
                                        type = c_app_server.
      gv_as_path = gs_config_details-value.
    ELSE.
      CALL 'C_SAPGPARAM' ID 'NAME'  FIELD 'DIR_HOME'
                          ID 'VALUE' FIELD gv_ftp_app_path.
    ENDIF.                             " IF GS_CONFIG_FILE-TYPE EQ ...

    IF gs_config_file-type EQ c_desktop.
      gv_desk = c_flag_on.
    ENDIF.                             " IF GS_CONFIG_FILE-TYPE EQ ...

    IF gs_config_file-type EQ c_ftp_on.
      gv_ftp = c_flag_on.
      READ TABLE gt_config_details INTO gs_config_details
                               WITH KEY mode = c_inbound
                                        type = c_ftp_host.
      gv_host = gs_config_details-value.
      READ TABLE gt_config_details INTO gs_config_details
                             WITH KEY mode = c_inbound
                                      type = c_ftp_uname.
      gv_user = gs_config_details-value.
      READ TABLE gt_config_details INTO gs_config_details
                             WITH KEY mode = c_inbound
                                      type = c_ftp_password.
      gv_pass = gs_config_details-value.
      READ TABLE gt_config_details INTO gs_config_details
                             WITH KEY mode = c_inbound
                                      type = c_ftp_dir.
      gv_cmd_ftppath = gs_config_details-value.
    ENDIF.                             " IF GS_CONFIG_FILE-TYPE CA ...
  ENDIF.                               " IF SY-SUBRC EQ 0.

ENDFORM.                               " FETCH_CONFIG_DETAILS

*----------------------------------------------------------------------*
*  Form  MODIFY_SCREEN                                                 *
*----------------------------------------------------------------------*
*  This subroutine is for modifying the selection screen based on quic-*
*  kpay configuration inputs                                           *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM modify_screen .

  gv_note = text-ffn.
  IF gv_desk IS INITIAL.
    LOOP AT SCREEN.
      IF screen-name EQ '%_P_DFILE_%_APP_%-TEXT'.
        screen-active = 0.
        MODIFY SCREEN.
      ENDIF.                           " SCREEN-NAME EQ '%_P_DPATH_%_...
      IF screen-name EQ 'P_DFILE'.
        screen-active = 0.
        MODIFY SCREEN.
      ENDIF.                           " SCREEN-NAME EQ 'P_DPATH'
      IF screen-name EQ 'GV_NOTE'.
        screen-active = 0.
        MODIFY SCREEN.
      ENDIF.                           " SCREEN-NAME EQ 'GV_NOTE'
    ENDLOOP.                           " LOOP AT SCREEN
  ELSE.
    LOOP AT SCREEN.
      IF screen-name EQ '%_P_FNAME_%_APP_%-TEXT'.
        screen-active = 0.
        MODIFY SCREEN.
      ENDIF.                           " SCREEN-NAME EQ '%_P_DPATH_%_...
      IF screen-name EQ 'P_FNAME'.
        screen-active = 0.
        MODIFY SCREEN.
      ENDIF.                           " SCREEN-NAME EQ 'P_DPATH'
    ENDLOOP.                           " LOOP AT SCREEN
  ENDIF.                               " IF GV_DESK IS INITIAL

ENDFORM.                               " MODIFY_SCREEN

*----------------------------------------------------------------------*
*  Form  BROWSE_FOR_FILE                                               *
*----------------------------------------------------------------------*
*  This subroutine is to browse the file                               *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM browse_for_file .

  CALL FUNCTION 'F4_FILENAME'
*   EXPORTING
*     PROGRAM_NAME        = SYST-CPROG
*     DYNPRO_NUMBER       = SYST-DYNNR
*     FIELD_NAME          = ' '
   IMPORTING
     file_name           = p_dfile.

ENDFORM.                               " BROWSE_FOR_FILE

*----------------------------------------------------------------------*
*  Form  CHECK_PATH_APPSERVER                                          *
*----------------------------------------------------------------------*
*  This subroutine is for validating error file application server path*
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM check_path_appserver USING iv_apppath TYPE string.

* local work variables .................................................

  DATA:
    lv_filepath TYPE ocs_file-name.    " File path

  DATA:
    lv_dir_files TYPE STANDARD TABLE
                        OF ocs_file INITIAL SIZE 0.

  lv_filepath = iv_apppath.

  CALL FUNCTION 'OCS_GET_FILE_INFO'
    EXPORTING
      dir_name                        = lv_filepath
*       FILE_NAME                       = '*'
    TABLES
      dir_list                        = lv_dir_files
    EXCEPTIONS
      no_authority                    = 1
      activity_unknown                = 2
      not_a_directory                 = 3
      no_media_in_drive               = 4
      too_many_errors                 = 5
      too_many_files                  = 6
      bracket_error_in_filename       = 7
      no_such_parameter               = 8
      OTHERS                          = 9.
  IF sy-subrc EQ 3.
    gv_stop_flag = c_flag_on.
    MESSAGE s016 WITH lv_filepath.
    CALL SELECTION-SCREEN 1000.
  ELSE.
    gv_stop_flag = c_flag_off.
  ENDIF.                               " IF SY-SUBRC EQ 3.

ENDFORM.                               " CHECK_PATH_APPSERVER

*----------------------------------------------------------------------*
*  Form  CHECK_INPUT_PAI                                               *
*----------------------------------------------------------------------*
*  This subroutine is to check the input values for PAI                *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM check_input_pai .

  IF gv_stop_flag EQ c_flag_on.
    LEAVE PROGRAM.
  ENDIF.

ENDFORM.                               " CHECK_INPUT_PAI

*----------------------------------------------------------------------*
*  Form  FETCH_DATA_FROM_SOURCE                                        *
*----------------------------------------------------------------------*
*  This subroutine is to fetch the data from source                    *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM fetch_data_from_source .

  IF gv_desk EQ c_flag_on.
    PERFORM check_file_path.
    PERFORM fetch_from_frontend.
  ENDIF.                               " IF GV_DESK EQ C_FLAG_ON.

  IF gv_as EQ c_flag_on.
* fetching the inbound file from app server to a internal table
    PERFORM fetch_data_from_server.
  ENDIF.                               " IF GV_AS EQ C_FLAG_ON.

  IF gv_ftp EQ c_flag_on.

* open the ftp connection
    PERFORM ftp_connection_open.

* fetching inbound csv file to app server from ftp location
    PERFORM fetch_file_from_ftp.

* close the ftp connection
    PERFORM ftp_connection_close.

* fetching the inbound file from app server to a internal table
    PERFORM fetch_data_from_server.

  ENDIF.                               " IF GV_FTP EQ C_FLAG_ON.

ENDFORM.                               " FETCH_DATA_FROM_SOURCE

*----------------------------------------------------------------------*
*  Form  FETCH_FROM_FRONTEND                                           *
*----------------------------------------------------------------------*
*  This subroutine is to fetch the data from frontend                  *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM fetch_from_frontend .

* Local work variables..................................................
  DATA:
    lv_file TYPE string,               " File path
    lv_string TYPE string,             " Text string
    lv_sreccount TYPE char10,          " Number of records
    lv_reccount TYPE i.                " Number of records

  lv_file = p_dfile.

  CALL FUNCTION 'GUI_UPLOAD'
    EXPORTING
      filename = lv_file
    TABLES
      data_tab = gt_file_data.

  LOOP AT gt_file_data INTO gs_file_data.
    SPLIT gs_file_data AT c_comma
                     INTO gs_payment_data-pbid
                          gs_payment_data-pbnam
                          gs_payment_data-xblnr
                          gs_payment_data-lifnr
                          gs_payment_data-bsnam
                          gs_payment_data-belnr
                          gs_payment_data-invid
                          gs_payment_data-bktxt
                          gs_payment_data-wrbtr
                          gs_payment_data-basid
                          gs_payment_data-sgtxt
                          gs_payment_data-disc
                          gs_payment_data-inco
                          gs_payment_data-oamt
                          gs_payment_data-bldat
                          gs_payment_data-pterm
                          gs_payment_data-waers
                          gs_payment_data-payamt
                          gs_payment_data-valut
                          gs_payment_data-doctyp
                          gs_payment_data-bmtid
                          gs_payment_data-btde
                          gs_payment_data-sgrp
                          gs_payment_data-zuonr
                          gs_payment_data-hbank.
    IF sy-subrc EQ 0.
      APPEND gs_payment_data TO gt_payment_data.
      CLEAR gs_payment_data.
    ELSE.
* filling the process details for extract log
      PERFORM fill_process_details USING text-afi.

      gv_status = text-fal.
* LOG file on Application Server.
      PERFORM create_appserver_log.
      MESSAGE e029.
    ENDIF.                             " IF SY-SUBRC EQ 0.
  ENDLOOP.                             " LOOP AT GT_FILE_DATA INTO ...

  DESCRIBE TABLE gt_payment_data LINES lv_reccount.

  lv_reccount = lv_reccount - 1.

  MESSAGE s014 WITH lv_reccount p_fname.

  lv_string = text-nrf.

  lv_sreccount = lv_reccount.

  CONDENSE lv_sreccount NO-GAPS.
  REPLACE ALL OCCURRENCES OF c_p1
                          IN lv_string
                        WITH lv_sreccount.

  REPLACE ALL OCCURRENCES OF c_p2
                          IN lv_string
                        WITH p_fname.

* filling the process details for extract log
  PERFORM fill_process_details USING lv_string.
ENDFORM.                               " FETCH_FROM_FRONTEND

*----------------------------------------------------------------------*
*  Form  CHECK_FILE_PATH                                               *
*----------------------------------------------------------------------*
*  This subroutine is to check the input file path                     *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM check_file_path .

* Local work variables..................................................
  DATA:
    lv_string TYPE string,             " Test string
    lv_flag TYPE char1 VALUE c_flag_off.
  " Flag

  CALL FUNCTION 'DX_FILE_EXISTENCE_CHECK'
    EXPORTING

      filename             = p_dfile
      pc                   = c_flag_on
*   SERVER              =
    IMPORTING
      file_exists          = lv_flag
    EXCEPTIONS
     rfc_error            = 1
     frontend_error       = 2
     no_authority         = 3
     OTHERS               = 4.

  IF NOT ( sy-subrc = 0 AND lv_flag = c_flag_on ).
    lv_string = text-ifn.
    REPLACE ALL OCCURRENCES OF c_p1
                            IN lv_string
                          WITH p_dfile.
* filling the process details for extract log
    PERFORM fill_process_details USING lv_string.

    gv_status = text-fal.
* LOG file on Application Server.
    PERFORM create_appserver_log.

    MESSAGE e020 WITH p_dfile.
  ELSE.
    lv_string = text-ifp.
    REPLACE ALL OCCURRENCES OF c_p1
                            IN lv_string
                          WITH p_dfile.
* filling the process details for extract log
    PERFORM fill_process_details USING lv_string.
    CLEAR lv_string.
  ENDIF.                               " IF NOT SY-SUBRC = 0 AND ...

ENDFORM.                               " CHECK_FILE_PATH

*----------------------------------------------------------------------*
*  Form  FETCH_ERROR_FILE_PATH                                         *
*----------------------------------------------------------------------*
*  This subroutine is to fetch the app server error file path          *
*----------------------------------------------------------------------*
*  There are no interface parameters to be passed on to the subroutine.*
*----------------------------------------------------------------------*
FORM fetch_error_file_path .

      CALL 'C_SAPGPARAM' ID 'NAME'  FIELD 'DIR_HOME'
                         ID 'VALUE' FIELD gv_as_epath.
      p_lfpath = gv_as_epath.
      p_efpath = gv_as_epath.

ENDFORM.                               " FETCH_ERROR_FILE_PATH
